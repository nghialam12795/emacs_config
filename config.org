#+AUTHOR: Nghia Lam
#+TITLE: PENMACS Configuration

[[./res/screenshots/workspace.png]]

* Table of Content                                                      :TOC:
- [[#about][About]]
- [[#emacs-startup][Emacs Startup]]
  - [[#system-information][System Information]]
  - [[#personal-information][Personal Information]]
  - [[#setting-default][Setting Default]]
  - [[#file-management][File Management]]
- [[#package-initialize][Package Initialize]]
- [[#advanced-config][Advanced Config]]
  - [[#autorevert][Autorevert]]
  - [[#calendar][Calendar]]
  - [[#dired][Dired]]
  - [[#fast-scroll][Fast Scroll]]
  - [[#editor-support][Editor Support]]
  - [[#eww][EWW]]
  - [[#ledger][Ledger]]
  - [[#git][Git]]
  - [[#ibuffer][Ibuffer]]
  - [[#ivy][Ivy]]
  - [[#hydra][Hydra]]
  - [[#paren][Paren]]
  - [[#pdf-reader][PDF Reader]]
  - [[#projectile][Projectile]]
  - [[#swiper][Swiper]]
  - [[#terminal][Terminal]]
  - [[#utilities][Utilities]]
  - [[#weather-forecast][Weather Forecast]]
  - [[#whitespace][Whitespace]]
- [[#language-support][Language Support]]
  - [[#c][C++]]
  - [[#cmake][CMake]]
  - [[#css][CSS]]
  - [[#csv][CSV]]
  - [[#elisp][Elisp]]
  - [[#json][Json]]
  - [[#html][HTML]]
  - [[#latex][Latex]]
  - [[#lua][Lua]]
  - [[#markdown][Markdown]]
  - [[#mermaid][Mermaid]]
  - [[#python][Python]]
  - [[#xml][XML]]
  - [[#yaml][YAML]]
- [[#user-interface][User Interface]]
  - [[#base][Base]]
  - [[#dashboard][Dashboard]]
  - [[#highlight][Highlight]]
  - [[#modeline][Modeline]]
  - [[#m-x-ui-enhancement][M-x UI Enhancement]]
  - [[#shackle][Shackle]]
  - [[#treemacs][Treemacs]]
  - [[#theme][Theme]]
  - [[#winner][Winner]]
- [[#org-mode][Org Mode]]
  - [[#sync-source-file][Sync Source File]]
  - [[#org][Org]]
  - [[#org-toc][Org TOC]]
  - [[#org-agenda][Org Agenda]]
  - [[#org-bullet][Org Bullet]]
  - [[#org-capture][Org Capture]]
  - [[#org-clock][Org Clock]]
  - [[#org-journal][Org Journal]]
  - [[#obx][Obx]]
  - [[#presentation][Presentation]]

* About
This is my configuration for the GNUs Emacs, which I call it PENMACS (Penguin x Emacs). WARNING: It's really long.
This configuration is based on my habit, which may not be suitable for some people. This config helps me in daily life works and personal notes.

For Emacs beginner, I'd recommend taking a look at [[https://github.com/hlissner/doom-emacs][doom-emacs]] or [[https://github.com/syl20bnr/spacemacs][Spacemacs]] (Vim Users).

My configuration is mostly inspired from:
- [[https://github.com/rememberYou/.emacs.d][Terencio Agozzino]]
- [[https://github.com/sachac/.emacs.d][Sacha Chua]]

You guys should check them out, they are both really talent. Especially, Sacha has a [[https://sachachua.com/blog/][blog]] about boosting Emacs in life.


* Emacs Startup
** System Information
Here I setup some info for my PENMACS. As my job requires me to work on all OS, you will see a lot of times I need to config for each of them differently.

#+begin_src emacs-lisp :tangle yes
(defvar pemacs/version
  "0.1.1"
  "Penguin Emacs version.")
(defconst my-homepage
  "https://github.com/nghialam12795"
  "My personal Github page.")
(defconst sys/win32
  (eq system-type 'windows-nt)
  "Are we using a Window OS?")
(defconst sys/linux
  (eq system-type 'gnu/linux)
  "Are we using a GNU/Linux OS?")
(defconst sys/macos
  (eq system-type 'darwin)
  "Are we using a Mac OS?")
(defconst emacs/>=26p
  (>= emacs-major-version 26)
  "Emacs is 26 or above.")
#+end_src

** Personal Information
#+begin_src emacs-lisp :tangle yes
(setq-default user-full-name "Nghia Lam"
              user-mail-address "nghialam12795@gmail.com")
#+end_src

** Setting Default
#+begin_src emacs-lisp :tangle yes
(advice-add #'display-startup-echo-area-message :override #'ignore)
(setq-default
  ad-redefinition-action 'accept                   ; Silence warnings for redefinition
  auto-window-vscroll nil                          ; Lighten vertical scroll
  bidi-display-reordering 'left-to-right           ; For optimization
  bidi-paragraph-direction 'left-to-right
  blink-matching-paren nil                         ; Dont blink mathcing paren
  confirm-kill-emacs 'yes-or-no-p                  ; Confirm before exiting Emacs
  cursor-in-non-selected-windows nil               ; Hide the cursor in inactive windows
  compilation-always-kill t                        ; Kill compilation process before starting another
  compilation-ask-about-save nil                   ; Save all buffers on `compile'
  compilation-scroll-output t
  delete-by-moving-to-trash t                      ; Delete files to trash
  display-time-default-load-average nil            ; Don't display load average
  display-time-format "%H:%M"                      ; Format the time string
  fill-column 80                                   ; Set width for automatic line breaks
  fringe-indicator-alist
    (delq (assq 'continuation
                fringe-indicator-alist)
          fringe-indicator-alist)                  ; Remove continuation arrow on the right frame
  help-window-select t                             ; Focus new help windows when opened
  indent-tabs-mode nil                             ; Stop using tabs to indent
  idle-update-delay 1                              ; Slow down update ui a bit
  inhibit-default-init t                           ; Default initialization
  inhibit-startup-message t                        ; Do not show anythings unnecessary
  initial-scratch-message nil                      ; Empty the initial *scratch* buffer
  inhibit-startup-echo-area-message user-full-name ; Show User name
  initial-major-mode 'fundamental-mode
  jit-lock-defer-time nil                          ; Font lock optimization
  jit-lock-stealth-nice 0.1
  jit-lock-stealth-time 0.2
  jit-lock-stealth-verbose nil
  ;; mode-line-format nil                             ; Disable mode line format when startup
  mouse-yank-at-point t                            ; Yank at point rather than pointer
  ns-use-srgb-colorspace nil                       ; Don't use sRGB colors
  recenter-positions '(5 top bottom)               ; Set re-centering positions
  ffap-machine-p-known 'reject                     ; Dont ping thing that look like domain
  frame-inhibit-implied-resize t                   ; Frame optimization
  highlight-nonselected-windows nil                ; Remove highlight on nonselected windows
  hscroll-margin 2                                 ; Horizontal scroll
  hscroll-step 1                                   ; Horizontal step
  fast-but-imprecise-scrolling t                   ; Fast scrolling
  recenter-positions '(top middle bottom)          ; Setup recenter
  scroll-conservatively most-positive-fixnum       ; Always scroll by one line
  scroll-margin 0                                  ; Add a margin when scrolling vertically
  scroll-preserve-screen-position t                ; Reduce cursor lag a bit
  mouse-wheel-scroll-amount '(5 ((shift) . 2))     ; Shift Mouse wheel
  mouse-wheel-progressive-speed nil                ; don't accelerate scrolling
  select-enable-clipboard t                        ; Merge system's and Emacs' clipboard
  sentence-end-double-space nil                    ; End a sentence after a dot and a space
  show-help-function nil                           ; Disable help messages
  show-trailing-whitespace nil                     ; Display trailing whitespaces
  split-height-threshold nil                       ; Disable vertical window splitting
  split-width-threshold nil                        ; Disable horizontal window splitting
  use-file-dialog nil                              ; Disable use file dialog
  use-dialog-box nil                               ; Disable use dialog box
  uniquify-buffer-name-style 'forward              ; Uniquify buffer names
  visible-bell 1                                   ; Turn off annoying sound
  window-combination-resize t                      ; Resize windows proportionally
  x-stretch-cursor t)                              ; Stretch cursor to the glyph width
(cd "~/")                                          ; Move to the user directory
(delete-selection-mode 1)                          ; Replace region when inserting text
(display-time-mode 1)                              ; Enable time in the mode-line
(fringe-mode 0)                                    ; Disable fringes
(fset 'yes-or-no-p 'y-or-n-p)                      ; Replace yes/no prompts with y/n
(global-subword-mode 1)                            ; Iterate through CamelCase words
(mouse-avoidance-mode 'banish)                     ; Avoid collision of mouse with point
(put 'downcase-region 'disabled nil)               ; Enable downcase-region
(put 'upcase-region 'disabled nil)                 ; Enable upcase-region
(prefer-coding-system 'utf-8-unix)
(set-locale-environment "en_US.UTF-8")
(set-default-coding-systems 'utf-8-unix)
(set-selection-coding-system 'utf-8-unix)
(set-buffer-file-coding-system 'utf-8-unix)
(set-clipboard-coding-system 'utf-8)               ; included by set-selection-coding-system
(set-keyboard-coding-system 'utf-8)                ; configured by prefer-coding-system
(set-terminal-coding-system 'utf-8)                ; configured by prefer-coding-system
(setq buffer-file-coding-system 'utf-8)            ; utf-8-unix
(setq save-buffer-coding-system 'utf-8-unix)       ; nil
(setq process-coding-system-alist
      (cons '("grep" utf-8 . utf-8) process-coding-system-alist))
(unless sys/macos (setq command-line-ns-option-alist nil))
(unless sys/linux (setq command-line-x-option-alist nil))
(toggle-frame-maximized)                           ; Toggle maximized

#+end_src

Some specific OS tweak:
#+begin_src emacs-lisp :tangle yes
(when sys/macos
  (setq mac-redisplay-dont-reset-vscroll t
        mac-mouse-wheel-smooth-scroll nil
        ns-pop-up-frames nil
        initial-frame-alist (append '((ns-transparent-titlebar . t)
                                      (ns-appearance . dark)
                                     )
                            )
  )
  (and (or (daemonp)
           (display-graphic-p))
       (require 'ns-auto-titlebar nil t)
       (ns-auto-titlebar-mode +1)
  )
  (setq mac-command-modifier 'meta) ; make cmd key do Meta
  (setq mac-option-modifier 'super) ; make opt key do Super
  (setq mac-control-modifier 'control) ; make Control key do Control
  (setq ns-function-modifier 'hyper)  ; make Fn key do Hyper
)
#+end_src
#+begin_src emacs-lisp :tangle yes
(when sys/win32
  (set-selection-coding-system 'utf-16-le)) ; For fixing broken symbol render in Windows. Any ideas why it happened?
#+end_src

Remove unnecessary error warnings
#+begin_src emacs-lisp :tangle yes
(defun penguin-command-error-function (data context caller)
  "Ignore the `buffer-read-only',`beginning-of-buffer',`end-of-buffer' signals.
Then pass DATA, CONTEXT & CALLER to the default handler."
  (when (not (memq (car data) '(buffer-read-only
                                beginning-of-buffer
                                end-of-buffer)))
    (command-error-default-function data context caller)))

(setq command-error-function #'penguin-command-error-function)
#+end_src

Other personal settings will be written to user.el
#+begin_src emacs-lisp :tangle yes
(setq custom-file (expand-file-name "~/.emacs.d/.local/user.el" user-emacs-directory))
;; If user.el is not there, duplicate the template for it
(let ((user-template-file
       (expand-file-name "user_template.el" user-emacs-directory)))
  (if (and (file-exists-p user-template-file)
           (not (file-exists-p custom-file)))
      (copy-file user-template-file custom-file)))

(if (file-exists-p custom-file)
    (load custom-file))
#+end_src

** File Management
I'd like to keep my folder as clean as possible. So I decide to move the auto-generated files around.

*** Folder structure
#+begin_src emacs-lisp :tangle yes
(defvar pemacs-dir (file-truename user-emacs-directory)
  "The path to this emacs.d directory.")
(defvar pcore-dir (concat pemacs-dir "core/")
  "Where essential files are stored.")
(defvar pmodules-dir (concat pemacs-dir "modules/")
  "Where configuration modules are stored.")
(defvar plocal-dir (concat pemacs-dir ".local/")
  "Root directory for local Emacs files.
Use this as permanent storage for files that are safe to share
across systems (if this config is symlinked across several computers).")
(defvar petc-dir (concat plocal-dir "etc/")
  "Directory for non-volatile storage.
Use this for files that don't change much, like servers binaries, external
dependencies or long-term shared data.")
(defvar pcache-dir (concat plocal-dir "cache/")
  "Directory for volatile storage.
Use this for files that change often, like cache files.")
#+end_src

*** Backup Location
#+begin_src emacs-lisp :tangle yes
(defun penguin_backup_file_name (fpath)
  "Backup files in a designated FPATH."
  (let* ((backupRootDir (concat plocal-dir "_backup"))
         (filePath (replace-regexp-in-string "[A-Za-z]:" "" fpath )) ; remove Windows driver letter in path, for example, ‚ÄúC:‚Äù
         (backupFilePath (replace-regexp-in-string "//" "/" (concat backupRootDir filePath "~") )))
         (make-directory (file-name-directory backupFilePath) (file-name-directory backupFilePath))
         backupFilePath))
(setq make-backup-file-name-function 'penguin_backup_file_name)
#+end_src

*** Others
#+begin_src emacs-lisp :tangle yes
(setq-default abbrev-file-name             (concat plocal-dir "abbrev.el")
              auto-save-list-file-name     (concat pcache-dir "autosave")
              pcache-directory             (concat pcache-dir "pcache/")
              recentf-save-file            (concat plocal-dir "recentf")
              mc/list-file                 (concat petc-dir "mc-lists.el")
              server-auth-dir              (concat pcache-dir "server/")
              shared-game-score-directory  (concat petc-dir "shared-game-score/")
              tramp-auto-save-directory    (concat pcache-dir "tramp-auto-save/")
              tramp-backup-directory-alist backup-directory-alist
              tramp-persistency-file-name  (concat pcache-dir "tramp-persistency.el")
              url-cache-directory          (concat pcache-dir "url/")
              url-configuration-directory  (concat petc-dir "url/")
              package-user-dir             (concat plocal-dir "packages"))
#+end_src


* Package Initialize
In order to install packages, use-package is a no-brainer for you.
But first, we need to add some sources
#+begin_src emacs-lisp :tangle yes
(package-initialize)
(setq package-archives '(("gnu"   . "http://elpa.gnu.org/packages/")
("MELPA Stable" . "https://stable.melpa.org/packages/")
                         ("melpa" . "http://melpa.org/packages/")
                         ("org"   . "http://orgmode.org/elpa/")))
#+end_src

Here goes our monster =use-package=
#+begin_src emacs-lisp :tangle yes
(unless (package-installed-p 'use-package)
  (package-refresh-contents)
  (package-install 'use-package))

(eval-and-compile
  (setq use-package-always-ensure t)
  (setq use-package-always-defer t)
  (setq use-package-always-demand nil)
  (setq use-package-expand-minimally t)
  (setq use-package-enable-imenu-support t))

(eval-when-compile
  (require 'use-package))

(use-package gcmh
  :ensure t
  :init
  (gcmh-mode 1))

(use-package quelpa
  :ensure t
  :defer t
  :custom
  (quelpa-update-melpa-p nil "Don't update the MELPA git repo."))

(use-package quelpa-use-package :ensure t)

;; Package Manager
(use-package paradox
  :custom
  (paradox-column-width-package 27)
  (paradox-column-width-version 13)
  (paradox-execute-asynchronously t)
  (paradox-github-token t)
  (paradox-hide-wiki-packages t)
  :config
  (remove-hook 'paradox-after-execute-functions #'paradox--report-buffer-print))
#+end_src


* Advanced Config
In editor war, the winner is not Emacs or Vim, it's your configuration with these two.
Now let's turn this Emacs into a real monster.

** Autorevert
Automatically reload files was modified by external program.
#+begin_src emacs-lisp :tangle yes
(use-package autorevert
  :ensure nil
  :diminish
  :hook (after-init . global-auto-revert-mode))
#+end_src

** Calendar
I usually let emacs fullscreen and open all the times, so I need to see the calendar inside Emacs.
#+begin_src emacs-lisp :tangle yes
(use-package calfw
  :commands cfw:open-calendar-buffer
  :bind ("<C-f11>" . open-calendar)
  :init
  (use-package calfw-org
    :commands (cfw:open-org-calendar cfw:org-create-source))

  (use-package calfw-ical
    :commands (cfw:open-ical-calendar cfw:ical-create-source))

  (defun open-calendar ()
    "Open calendar."
    (interactive)
    (unless (ignore-errors
              (cfw:open-calendar-buffer
               :contents-sources
               (list
                (when org-agenda-files
                  (cfw:org-create-source "YellowGreen"))
                (when (bound-and-true-p centaur-ical)
                  (cfw:ical-create-source "gcal" centaur-ical "IndianRed")))))
      (cfw:open-calendar-buffer))))
#+end_src

** Dired
Emacs is also a file explorer alternative.
#+begin_src emacs-lisp :tangle yes
(use-package dired
  :ensure nil
  :preface
  (defun penguin/dired-directories-first ()
    "Sort dired listings with directories first before adding marks."
    (save-excursion
      (let (buffer-read-only)
        (forward-line 2)
        (sort-regexp-fields t "^.*$" "[ ]*." (point) (point-max)))
      (set-buffer-modified-p nil)))
  :hook
  (dired-mode . dired-hide-details-mode)
  :custom
  (dired-auto-revert-buffer t)
  (dired-dwim-target t)
  (dired-hide-details-hide-symlink-targets nil)
  (dired-listing-switches "-alh")
  (dired-ls-F-marks-symlinks nil)
  (dired-recursive-copies 'always)
  :config
  (advice-add 'dired-readin :after #'penguin/dired-directories-first))
(use-package dired-subtree
  :bind (:map dired-mode-map
              ("<backtab>" . dired-subtree-cycle)
              ("<tab>" . dired-subtree-toggle)))
(use-package dired-git-info
  :ensure t
  :after dired
  :config
  (setq dgi-commit-message-format "%h\t%s\t%cr")
  :bind (:map dired-mode-map
              (")" . dired-git-info-mode)))
#+end_src

** Fast Scroll
Scrolling through a large buffer cause my MacOS laggy. This package can prevent that.
#+begin_src emacs-lisp :tangle yes
(use-package fast-scroll
  :ensure t
  :diminish fast-scroll-mode
  :config
  (fast-scroll-config)
  (fast-scroll-mode 1))
#+end_src

** Editor Support
*** Ace-jump
Jumping around the workspace for quickly editing is a big plus. Say NO to mouse.
#+begin_src emacs-lisp :tangle yes
(require 'ace-jump-mode)
(eval-after-load "ace-jump-mode" '(ace-jump-mode-enable-mark-sync))
(define-key global-map (kbd "C-j") 'ace-jump-mode)
(define-key global-map (kbd "C-x SPC") 'ace-jump-mode-pop-mark)
#+end_src

*** Autocomplete
=company= is a powerful package provide an auto-completion at point, which display a small pop-in containing the candidate.
I also =company-box= that allow a company front-end with more compact UI.
#+begin_src emacs-lisp :tangle yes
(use-package company
  :diminish
  :defines (company-dabbrev-ignore-case company-dabbrev-downcase)
  :commands company-abort
  :bind (("M-/" . company-complete)
         ("<backtab>" . company-yasnippet)
         :map company-active-map
         ("C-p" . company-select-previous)
         ("C-n" . company-select-next)
         ("<tab>" . company-complete-common-or-cycle)
         ("<backtab>" . my-company-yasnippet)
         ;; ("C-c C-y" . my-company-yasnippet)
         :map company-search-map
         ("C-p" . company-select-previous)
         ("C-n" . company-select-next))
  :hook (after-init . global-company-mode)
  :init
  (defun my-company-yasnippet ()
    "Hide the current completeions and show snippets."
    (interactive)
    (company-abort)
    (call-interactively 'company-yasnippet))
  :config
  (setq company-tooltip-align-annotations t
        company-tooltip-limit 12
        company-idle-delay 0
        company-echo-delay (if (display-graphic-p) nil 0)
        company-minimum-prefix-length 2
        company-require-match nil
        company-dabbrev-ignore-case nil
        company-dabbrev-downcase nil)

  ;; Better sorting and filtering
  (use-package company-prescient
    :init (company-prescient-mode 1))

  ;; Icons and quickhelp
  (when emacs/>=26p
    (use-package company-box
      :diminish
      :hook (company-mode . company-box-mode)
      :init (setq company-box-backends-colors nil
                  company-box-show-single-candidate t
                  company-box-max-candidates 50
                  company-box-doc-delay 0.5)
      :config
      (with-no-warnings
        ;; Highlight `company-common'
        (defun my-company-box--make-line (candidate)
          (-let* (((candidate annotation len-c len-a backend) candidate)
                  (color (company-box--get-color backend))
                  ((c-color a-color i-color s-color) (company-box--resolve-colors color))
                  (icon-string (and company-box--with-icons-p (company-box--add-icon candidate)))
                  (candidate-string (concat (propertize (or company-common "") 'face 'company-tooltip-common)
                                            (substring (propertize candidate 'face 'company-box-candidate)
                                                       (length company-common) nil)))
                  (align-string (when annotation
                                  (concat " " (and company-tooltip-align-annotations
                                                   (propertize " " 'display `(space :align-to (- right-fringe ,(or len-a 0) 1)))))))
                  (space company-box--space)
                  (icon-p company-box-enable-icon)
                  (annotation-string (and annotation (propertize annotation 'face 'company-box-annotation)))
                  (line (concat (unless (or (and (= space 2) icon-p) (= space 0))
                                  (propertize " " 'display `(space :width ,(if (or (= space 1) (not icon-p)) 1 0.75))))
                                (company-box--apply-color icon-string i-color)
                                (company-box--apply-color candidate-string c-color)
                                align-string
                                (company-box--apply-color annotation-string a-color)))
                  (len (length line)))
            (add-text-properties 0 len (list 'company-box--len (+ len-c len-a)
                                             'company-box--color s-color)
                                 line)
            line))
        (advice-add #'company-box--make-line :override #'my-company-box--make-line)

        ;; Prettify icons
        (defun my-company-box-icons--elisp (candidate)
          (when (derived-mode-p 'emacs-lisp-mode)
            (let ((sym (intern candidate)))
              (cond ((fboundp sym) 'Function)
                    ((featurep sym) 'Module)
                    ((facep sym) 'Color)
                    ((boundp sym) 'Variable)
                    ((symbolp sym) 'Text)
                    (t . nil)))))
        (advice-add #'company-box-icons--elisp :override #'my-company-box-icons--elisp))

      (when (and (display-graphic-p)
                 (require 'all-the-icons nil t))
        (declare-function all-the-icons-faicon 'all-the-icons)
        (declare-function all-the-icons-material 'all-the-icons)
        (declare-function all-the-icons-octicon 'all-the-icons)
        (setq company-box-icons-all-the-icons
              `((Unknown . ,(all-the-icons-material "find_in_page" :height 0.85 :v-adjust -0.2))
                (Text . ,(all-the-icons-faicon "text-width" :height 0.8 :v-adjust -0.05))
                (Method . ,(all-the-icons-faicon "cube" :height 0.8 :v-adjust -0.05 :face 'all-the-icons-purple))
                (Function . ,(all-the-icons-faicon "cube" :height 0.8 :v-adjust -0.05 :face 'all-the-icons-purple))
                (Constructor . ,(all-the-icons-faicon "cube" :height 0.8 :v-adjust -0.05 :face 'all-the-icons-purple))
                (Field . ,(all-the-icons-octicon "tag" :height 0.8 :v-adjust 0 :face 'all-the-icons-lblue))
                (Variable . ,(all-the-icons-octicon "tag" :height 0.8 :v-adjust 0 :face 'all-the-icons-lblue))
                (Class . ,(all-the-icons-material "settings_input_component" :height 0.85 :v-adjust -0.2 :face 'all-the-icons-orange))
                (Interface . ,(all-the-icons-material "share" :height 0.85 :v-adjust -0.2 :face 'all-the-icons-lblue))
                (Module . ,(all-the-icons-material "view_module" :height 0.85 :v-adjust -0.2 :face 'all-the-icons-lblue))
                (Property . ,(all-the-icons-faicon "wrench" :height 0.8 :v-adjust -0.05))
                (Unit . ,(all-the-icons-material "settings_system_daydream" :height 0.85 :v-adjust -0.2))
                (Value . ,(all-the-icons-material "format_align_right" :height 0.85 :v-adjust -0.2 :face 'all-the-icons-lblue))
                (Enum . ,(all-the-icons-material "storage" :height 0.85 :v-adjust -0.2 :face 'all-the-icons-orange))
                (Keyword . ,(all-the-icons-material "filter_center_focus" :height 0.85 :v-adjust -0.2))
                (Snippet . ,(all-the-icons-material "format_align_center" :height 0.85 :v-adjust -0.2))
                (Color . ,(all-the-icons-material "palette" :height 0.85 :v-adjust -0.2))
                (File . ,(all-the-icons-faicon "file-o" :height 0.85 :v-adjust -0.05))
                (Reference . ,(all-the-icons-material "collections_bookmark" :height 0.85 :v-adjust -0.2))
                (Folder . ,(all-the-icons-faicon "folder-open" :height 0.85 :v-adjust -0.05))
                (EnumMember . ,(all-the-icons-material "format_align_right" :height 0.85 :v-adjust -0.2 :face 'all-the-icons-lblue))
                (Constant . ,(all-the-icons-faicon "square-o" :height 0.85 :v-adjust -0.05))
                (Struct . ,(all-the-icons-material "settings_input_component" :height 0.85 :v-adjust -0.2 :face 'all-the-icons-orange))
                (Event . ,(all-the-icons-octicon "zap" :height 0.8 :v-adjust 0 :face 'all-the-icons-orange))
                (Operator . ,(all-the-icons-material "control_point" :height 0.85 :v-adjust -0.2))
                (TypeParameter . ,(all-the-icons-faicon "arrows" :height 0.8 :v-adjust -0.05))
                (Template . ,(all-the-icons-material "format_align_center" :height 0.85 :v-adjust -0.2)))
              company-box-icons-alist 'company-box-icons-all-the-icons))))

  ;; Popup documentation for completion candidates
  (when (and (not emacs/>=26p) (display-graphic-p))
    (use-package company-quickhelp
      :defines company-quickhelp-delay
      :bind (:map company-active-map
             ([remap company-show-doc-buffer] . company-quickhelp-manual-begin))
      :hook (global-company-mode . company-quickhelp-mode)
      :init (setq company-quickhelp-delay 0.5))))

#+end_src

*** Delsel
=C-c C-g= will always quit the minibuffer.
#+begin_src emacs-lisp :tangle yes
(use-package delsel
  :bind
  (:map mode-specific-map
        ("C-g" . minibuffer-keyboard-quit)))
#+end_src

*** Docsets
I use [[https://github.com/chubin/cheat.sh][cheat.sh]] as my quick cheatsheets and =counsel-dash= to browse the complete dash docsets.
You'd need to install the docsets you want with =dash-doc-install-docsets= command. For me, I work mostly with *C++* so I just need it's docsets.
#+begin_src emacs-lisp :tangle yes
(use-package helm-dash)
(use-package counsel-dash)
(add-hook 'emacs-lisp-mode-hook '(lambda ()
                                   (setq-local counsel-dash-docsets '("Emacs Lisp"))
                                   (setq helm-current-buffer (current-buffer))))
(add-hook 'c-mode-common-hook '(lambda ()
                                 (setq-local counsel-dash-docsets '("C++"))
                                 (setq helm-current-buffer (current-buffer))))

(global-set-key (kbd "M-h")  'counsel-dash-at-point)

(setq dash-docs-docsets-path "~/.emacs.d/.docsets")
;; (setq dash-docs-browser-func 'eww-browse-url)
(setq counsel-dash-min-length 3)
(setq counsel-dash-candidate-format "%d %n (%t)")
(setq counsel-dash-enable-debugging nil)
(setq counsel-dash-ignored-docsets nil)
#+end_src

*** Editor Config
[[https://editorconfig.org][EditorConfig]] helps maintain consistent coding styles for multiple developers working on the same project across various editors and IDEs.
#+begin_src emacs-lisp :tangle yes
  (use-package editorconfig
    :defer 0.3
    :config (editorconfig-mode 1))
#+end_src

*** LSP Mode
In order to be able to use different LSP (Language Server Protocol) server according to the programming language that we want to use, we need a client for LSP. That‚Äôs where lsp-mode comes in!
#+begin_src emacs-lisp :tangle yes
  (use-package lsp-mode
    :commands lsp
    ;; reformat code and add missing (or remove old) imports
    :hook ((before-save . lsp-format-buffer)
           (before-save . lsp-organize-imports)
           (lsp-mode . lsp-enable-which-key-integration))
    :bind (("C-c d" . lsp-describe-thing-at-point)
           ("C-c e n" . flymake-goto-next-error)
           ("C-c e p" . flymake-goto-prev-error)
           ("C-c e r" . lsp-find-references)
           ("C-c e R" . lsp-rename)
           ("C-c e i" . lsp-find-implementation)
           ("C-c e t" . lsp-find-type-definition))
    :config
    (setq lsp-log-io nil)
    (setq lsp-diagnostic-package :none)
    (setq lsp-enable-links nil)
    (setq lsp-restart 'auto-restart)
    (setq lsp-client-packages '(lsp-clients))
    (push "[/\\\\][^/\\\\]*\\.\\(json\\|html\\|jade\\)$" lsp-file-watch-ignored) ; json
    (defvar lsp-on-touch-time 0)
    (defadvice lsp-on-change (around lsp-on-change-hack activate)
      ;; don't run `lsp-on-change' too frequently
      (when (> (- (float-time (current-time))
                  lsp-on-touch-time) 30) ;; 30 seconds
        (setq lsp-on-touch-time (float-time (current-time)))
        ad-do-it)))


  (use-package lsp-ui
    :custom-face
    (lsp-ui-doc-background ((t (:background ,(face-background 'tooltip)))))
    (lsp-ui-sideline-code-action ((t (:inherit warning))))
    :bind (("C-c u" . lsp-ui-imenu)
           :map lsp-ui-mode-map
           ("C-c h l" . hydra-lspui/body)
           ([remap xref-find-definitions] . lsp-ui-peek-find-definitions)
           ([remap xref-find-references] . lsp-ui-peek-find-references))
    :init (setq lsp-ui-doc-enable t
                lsp-ui-doc-use-webkit nil
                lsp-ui-doc-delay 0.2
                lsp-ui-doc-include-signature t
                lsp-ui-doc-position 'at-point
                lsp-ui-doc-border (face-foreground 'default)
                lsp-eldoc-enable-hover nil ; Disable eldoc displays in minibuffer

                lsp-ui-flycheck-enable t

                lsp-ui-imenu-enable t
                lsp-ui-imenu-kind-position 'top
                lsp-ui-imenu-colors `(,(face-foreground 'font-lock-keyword-face)
                                      ,(face-foreground 'font-lock-string-face)
                                      ,(face-foreground 'font-lock-constant-face)
                                      ,(face-foreground 'font-lock-variable-name-face))

                lsp-ui-sideline-enable nil
                lsp-ui-sideline-ignore-duplicate t
                lsp-ui-sideline-show-symbol t
                lsp-ui-sideline-show-hover t
                lsp-ui-sideline-show-diagnostics nil
                lsp-ui-sideline-show-code-actions t

                lsp-ui-peek-enable t
                lsp-ui-peek-peek-height 20
                lsp-ui-peek-list-width 50
                lsp-ui-peek-fontify 'on-demand ) ;; never, on-demand, or always
    :config
    (add-to-list 'lsp-ui-doc-frame-parameters '(right-fringe . 8))
    ;; `C-g'to close doc
    (advice-add #'keyboard-quit :before #'lsp-ui-doc-hide)
    ;; Reset `lsp-ui-doc-background' after loading theme
    (add-hook 'after-load-theme-hook
              (lambda ()
                (setq lsp-ui-doc-border (face-foreground 'default))
                (set-face-background 'lsp-ui-doc-background
                                     (face-background 'tooltip))))
    ;; WORKAROUND Hide mode-line of the lsp-ui-imenu buffer
    ;; @see https://github.com/emacs-lsp/lsp-ui/issues/243
    (defun penguin/lsp-ui-imenu-hide-mode-line ()
      "Hide the mode-line in lsp-ui-imenu."
      (setq mode-line-format nil))
    (advice-add #'lsp-ui-imenu :after #'penguin/lsp-ui-imenu-hide-mode-line))

  (use-package company-lsp
    ;; company-mode completion
    :commands company-lsp)

  (use-package lsp-treemacs
    ;; project wide overview
    :commands lsp-treemacs-errors-list)

  (use-package lsp-ivy :commands lsp-ivy-workspace-symbol)

  (use-package dap-mode
    :after lsp-mode
    :config
    (dap-mode t)
    (dap-ui-mode t))
  (add-hook 'prog-mode-hook #'lsp)
#+end_src

*** Linter
Flycheck is a linters for showing errors directly within buffers.
#+begin_src emacs-lisp :tangle yes
  (use-package flycheck
    :diminish
    :hook (after-init . global-flycheck-mode)
    :config
    (setq flycheck-emacs-lisp-load-path 'inherit)

    ;; Only check while saving and opening files
    (setq flycheck-check-syntax-automatically '(save mode-enabled))

    ;; Set fringe style
    (setq flycheck-indication-mode 'right-fringe)
    (when (fboundp 'define-fringe-bitmap)
      (define-fringe-bitmap 'flycheck-fringe-bitmap-double-arrow
        [16 48 112 240 112 48 16] nil nil 'center))

    ;; Display Flycheck errors in GUI tooltips
    (if (display-graphic-p)
        (if emacs/>=26p
            (use-package flycheck-posframe
              :custom-face (flycheck-posframe-border-face ((t (:inherit default))))
              :hook (flycheck-mode . flycheck-posframe-mode)
              :init (setq flycheck-posframe-border-width 1
                          flycheck-posframe-inhibit-functions
                          '((lambda (&rest _) (bound-and-true-p company-backend)))))
          (use-package flycheck-pos-tip
            :defines flycheck-pos-tip-timeout
            :hook (global-flycheck-mode . flycheck-pos-tip-mode)
            :config (setq flycheck-pos-tip-timeout 30)))
      (use-package flycheck-popup-tip
        :hook (flycheck-mode . flycheck-popup-tip-mode))))

  (use-package flymake
    :commands flymake-mode)

  (add-hook 'c++-mode-hook 'flycheck-mode)
  (add-hook 'c-mode-hook 'flycheck-mode)
#+end_src

*** Move Text
Not the best option for hotkey right now ...
#+begin_src emacs-lisp :tangle yes
;; `Move text'
(use-package move-text
  :bind (("M-p" . move-text-up)
         ("M-n" . move-text-down))
  :config (move-text-default-bindings))
#+end_src

*** Multiple Cursors
A really great package for editing. I use it all the time for editing multiple line or replace a symbol.
#+begin_src emacs-lisp :tangle yes
(use-package multiple-cursors)
(global-set-key (kbd "C->") 'mc/mark-next-like-this)
(global-set-key (kbd "C-<") 'mc/mark-previous-like-this)
(global-set-key (kbd "C-c C-<") 'mc/mark-all-like-this)
#+end_src

*** Scrolling with keys
Let's make scrolling right.
#+begin_src emacs-lisp :tangle yes
(defun push-mark-no-activate ()
  "Pushes `point` to `mark-ring' and does not activate the region.
Equivalent to \\[set-mark-command] when \\[transient-mark-mode] is disabled"
  (interactive)
  (push-mark (point) t nil)) ; removed the message, visible-mark takes care of this

(defun penguin/scroll-down-with-mark ()
  "Like `scroll-down-command`, but push a mark if this is not a repeat invocation."
  (interactive)
  (unless (equal last-command 'penguin/scroll-down-with-mark)
    (push-mark-no-activate))
  (scroll-down-command))

(defun penguin/scroll-up-with-mark ()
  "Like `scroll-up-command`, but push a mark if this is not a repeat invocation."
  (interactive)
  (unless (equal last-command 'penguin/scroll-up-with-mark)
    (push-mark-no-activate))
  (scroll-up-command))

(global-set-key (kbd "C-v") 'penguin/scroll-up-with-mark)
(global-set-key (kbd "M-v") 'penguin/scroll-down-with-mark)
#+end_src

*** Smart Comment
#+begin_src emacs-lisp :tangle yes
(use-package smart-comment
  :bind ("M-;" . smart-comment))
#+end_src

*** Snippet
A good IDE always come with a good snippet configuration.
#+begin_src emacs-lisp :tangle yes
(use-package yasnippet-snippets
  :ensure t
  :after yasnippet
  :config (yasnippet-snippets-initialize))

(use-package yasnippet
  :delight yas-minor-mode " œÖ"
  :hook (yas-minor-mode . penguin/disable-yas-if-no-snippets)
  :config (yas-global-mode)
  :preface
  (defun penguin/disable-yas-if-no-snippets ()
    (when (and yas-minor-mode (null (yas--get-snippet-tables)))
      (yas-minor-mode -1))))

(use-package ivy-yasnippet :after yasnippet)
(use-package react-snippets :after yasnippet)
#+end_src

*** Utilities
=which-key= show me the guideline for every next hotkey of the combo.
#+begin_src emacs-lisp :tangle yes
(use-package which-key
  :diminish
  :config
  (setq which-key-idle-delay 0.4
        which-key-idle-secondary-delay 0.4))
(which-key-mode 1)
#+end_src

Sometimes I feel stupid at coding. I just duplicate a line to a line and not thinking much about optimization.
But it's quick üòÅ.

#+begin_src emacs-lisp :tangle yes
(defun penguin/quick-dup-line ()
  "Quickly duplicate the current line down."
  (interactive)
  (let ((beg (line-beginning-position 1))
        (end (line-beginning-position 2)))
    (if (eq last-command 'quick-copy-line)
        (kill-append (buffer-substring beg end) (< end beg))
      (kill-new (buffer-substring beg end))))
  (beginning-of-line 2)
  (yank)) ;; Can be duplicated more with `C-y'

(global-set-key (kbd "C-c C-d") 'penguin/quick-dup-line)
#+end_src

A more convinient =C-a=
#+begin_src emacs-lisp :tangle yes
(global-set-key [remap move-beginning-of-line] #'penguin/beginning-of-line-dwim)
(defun penguin/beginning-of-line-dwim ()
  "Move point to first non-whitespace character, or beginning of line."
  (interactive "^")
  (let ((origin (point)))
    (beginning-of-line)
    (and (= origin (point))
         (back-to-indentation))))
#+end_src

Remove unnecessary keymap
#+begin_src emacs-lisp :tangle yes
(global-unset-key (kbd "C-z"))
(global-unset-key (kbd "C-x C-z"))
(global-unset-key (kbd "C-h h"))
#+end_src

Setup keymap for my habit
#+begin_src emacs-lisp :tangle yes
(define-key global-map (kbd "C-G") 'ff-find-other-file)

(global-set-key (kbd "C-+") 'text-scale-increase)
(global-set-key (kbd "C--") 'text-scale-decrease)

(global-set-key [remap kill-buffer] #'kill-this-buffer)

(global-set-key (kbd "C-x 3") (lambda () (interactive)(split-window-right) (other-window 1)))
(global-set-key (kbd "C-x 2") (lambda () (interactive)(split-window-below) (other-window 1)))

(global-set-key (kbd "M-o") 'other-window)
(global-set-key (kbd "M-O") 'other-frame)
(global-set-key (kbd "M-N") 'next-buffer)
(global-set-key (kbd "M-P") 'previous-buffer)

(global-set-key (kbd "C-c C-b")  'windmove-left)
(global-set-key (kbd "C-c C-f") 'windmove-right)
(global-set-key (kbd "C-c C-p")    'windmove-up)
(global-set-key (kbd "C-c C-n")  'windmove-down)
#+end_src

** EWW
Working in Emacs is fun, but switching to safari or chrome to do searching stuff sometimes annoy me. As I dont like to use mouse much.
=eww= is not perfect but it can solve my current nerve right now. I'd like to learning using qutebrowser someday as a replacement.
#+begin_src emacs-lisp :tangle yes
  ;; (use-package browse-url
  ;;   :ensure nil
  ;;   :custom
  ;;   (browse-url-browser-function 'eww-browse-url)
  ;;   ;; (browse-url-browser-function 'browse-url-generic)
  ;;   ;; :config
  ;;   ;; (cond (sys/win32 (setq browse-url-generic-program qutebrowser/win32))
  ;;   ;;       (sys/macos (setq browse-url-generic-program qutebrowser/macos))
  ;;   ;;       (sys/linux (setq browse-url-generic-program "qutebrowser"))
  ;;   ;; )
  ;; )

  (use-package shr
    :commands (eww
               eww-browse-url)
    :config
    (setq browse-url-browser-function 'eww-browse-url)
    (setq shr-use-fonts nil)
    (setq shr-use-colors nil)
    (setq shr-max-image-proportion 0.2)
    (setq shr-width (current-fill-column)))

  (use-package shr-tag-pre-highlight
    :ensure t
    :after shr
    :config
    (add-to-list 'shr-external-rendering-functions
                 '(pre . shr-tag-pre-highlight))
    (when (version< emacs-version "26")
      (with-eval-after-load 'eww
        (advice-add 'eww-display-html :around
                    'eww-display-html--override-shr-external-rendering-functions))))
#+end_src

** Ledger
**Ledger** is a system for finance management, which is pretty good management flow for someone carefree like me.
#+begin_src emacs-lisp :tangle yes
  (use-package ledger-mode
    :mode ("\\.dat\\'"
           "\\.ledger\\'")
    :bind (:map ledger-mode-map
                ("C-x C-s" . penguin/ledger-save))
    :hook (ledger-mode . ledger-flymake-enable)
    :preface
    (defun penguin/ledger-save ()
      "Automatically clean the ledger buffer at each save."
      (interactive)
      (ledger-mode-clean-buffer)
      (save-buffer))
    :custom
    (ledger-clear-whole-transactions t)
    (ledger-reconcile-default-commodity "EUR")
    (ledger-reports
     '(("account statement" "%(binary) reg --real [[ledger-mode-flags]] -f %(ledger-file) ^%(account)")
       ("balance sheet" "%(binary) --real [[ledger-mode-flags]] -f %(ledger-file) bal ^assets ^liabilities ^equity")
       ("budget" "%(binary) --empty -S -T [[ledger-mode-flags]] -f %(ledger-file) bal ^assets:bank ^assets:receivables ^assets:cash ^assets:budget")
       ("budget goals" "%(binary) --empty -S -T [[ledger-mode-flags]] -f %(ledger-file) bal ^assets:bank ^assets:receivables ^assets:cash ^assets:'budget goals'")
       ("budget obligations" "%(binary) --empty -S -T [[ledger-mode-flags]] -f %(ledger-file) bal ^assets:bank ^assets:receivables ^assets:cash ^assets:'budget obligations'")
       ("budget debts" "%(binary) --empty -S -T [[ledger-mode-flags]] -f %(ledger-file) bal ^assets:bank ^assets:receivables ^assets:cash ^assets:'budget debts'")
       ("cleared" "%(binary) cleared [[ledger-mode-flags]] -f %(ledger-file)")
       ("equity" "%(binary) --real [[ledger-mode-flags]] -f %(ledger-file) equity")
       ("income statement" "%(binary) --invert --real -S -T [[ledger-mode-flags]] -f %(ledger-file) bal ^income ^expenses -p \"this month\""))
     (ledger-report-use-header-line nil)))

  (use-package flycheck-ledger :after ledger-mode)
#+end_src

** Git
My setup for git is kinda complex as my habit of merging and braching seem too random. Hopefully, I will fix this sometimes ...
Now, the first part is we need to have =magit=
#+begin_src emacs-lisp :tangle yes
(use-package magit
  :bind
  ("C-x g" . magit-status)
  (:map magit-hunk-section-map
        ("RET" . magit-diff-visit-file-other-window)
        ([return] . magit-diff-visit-file-other-window))
  :custom
  (magit-display-buffer-function 'magit-display-buffer-same-window-except-diff-v1)
  (magit-diff-highlight-hunk-body nil)
  (magit-diff-highlight-hunk-region-functions
   '(magit-diff-highlight-hunk-region-dim-outside magit-diff-highlight-hunk-region-using-face))
  (magit-popup-display-buffer-action '((display-buffer-same-window)))
  (magit-refs-show-commit-count 'all)
  (magit-section-show-child-count t)
  :config
  (remove-hook 'magit-section-highlight-hook #'magit-section-highlight))
#+end_src

A simple workflow for git need commit and message. These two will setup for us.
#+begin_src emacs-lisp :tangle yes
(use-package git-commit
  :preface
  (defun penguin/git-commit-auto-fill-everywhere ()
    (setq fill-column 72)
    (setq-local comment-auto-fill-only-comments nil))
  :hook
  (git-commit-mode . penguin/git-commit-auto-fill-everywhere)
  :custom
  (git-commit-summary-max-length 50))

(use-package git-messenger
  :bind (:map vc-prefix-map
         ("p" . git-messenger:popup-message)
         :map git-messenger-map
         ("m" . git-messenger:copy-message))
  :init (setq git-messenger:show-detail t
              git-messenger:use-magit-popup t)
  :config
  (with-no-warnings
    (with-eval-after-load 'hydra
      (defhydra git-messenger-hydra (:color blue)
        ("s" git-messenger:popup-show "show")
        ("c" git-messenger:copy-commit-id "copy hash")
        ("m" git-messenger:copy-message "copy message")
        ("," (catch 'git-messenger-loop (git-messenger:show-parent)) "go parent")
        ("q" git-messenger:popup-close "quit")))

    (defun penguin-git-mess:format-detail (vcs commit-id author message)
      (if (eq vcs 'git)
          (let ((date (git-messenger:commit-date commit-id))
                (colon (propertize ":" 'face 'font-lock-comment-face)))
            (concat
             (format "%s%s %s \n%s%s %s\n%s  %s %s \n"
                     (propertize "Commit" 'face 'font-lock-keyword-face) colon
                     (propertize (substring commit-id 0 8) 'face 'font-lock-comment-face)
                     (propertize "Author" 'face 'font-lock-keyword-face) colon
                     (propertize author 'face 'font-lock-string-face)
                     (propertize "Date" 'face 'font-lock-keyword-face) colon
                     (propertize date 'face 'font-lock-string-face))
             (propertize (make-string 38 ?‚îÄ) 'face 'font-lock-comment-face)
             message
             (propertize "\nPress q to quit" 'face '(:inherit (font-lock-comment-face italic)))))
        (git-messenger:format-detail vcs commit-id author message)))

    (defun penguin-git-mess:popup-message ()
      "Popup message with `posframe', `pos-tip', `lv' or `message', and dispatch actions with `hydra'."
      (interactive)
      (let* ((vcs (git-messenger:find-vcs))
             (file (buffer-file-name (buffer-base-buffer)))
             (line (line-number-at-pos))
             (commit-info (git-messenger:commit-info-at-line vcs file line))
             (commit-id (car commit-info))
             (author (cdr commit-info))
             (msg (git-messenger:commit-message vcs commit-id))
             (popuped-message (if (git-messenger:show-detail-p commit-id)
                                  (penguin-git-mess:format-detail vcs commit-id author msg)
                                (cl-case vcs
                                  (git msg)
                                  (svn (if (string= commit-id "-")
                                           msg
                                         (git-messenger:svn-message msg)))
                                  (hg msg)))))
        (setq git-messenger:vcs vcs
              git-messenger:last-message msg
              git-messenger:last-commit-id commit-id)
        (run-hook-with-args 'git-messenger:before-popup-hook popuped-message)
        (git-messenger-hydra/body)
        (cond ((and (fboundp 'posframe-workable-p) (posframe-workable-p))
               (let ((buffer-name "*git-messenger*"))
                 (posframe-show buffer-name
                                :string popuped-message
                                :left-fringe 8
                                :right-fringe 8
                                :internal-border-color (face-foreground 'default)
                                :internal-border-width 1)
                 (unwind-protect
                     (push (read-event) unread-command-events)
                   (posframe-delete buffer-name))))
              ((and (fboundp 'pos-tip-show) (display-graphic-p))
               (pos-tip-show popuped-message))
              ((fboundp 'lv-message)
               (lv-message popuped-message)
               (unwind-protect
                   (push (read-event) unread-command-events)
                 (lv-delete-window)))
              (t (message "%s" popuped-message)))
        (run-hook-with-args 'git-messenger:after-popup-hook popuped-message)))
    (advice-add #'git-messenger:popup-close :override #'ignore)
    (advice-add #'git-messenger:popup-message :override #'penguin-git-mess:popup-message)))

#+end_src

To see revisions of a file, =git-timemachine= is needed.
#+begin_src emacs-lisp :tangle yes
(use-package git-timemachine
  :custom-face
  (git-timemachine-minibuffer-author-face ((t (:inherit success))))
  (git-timemachine-minibuffer-detail-face ((t (:inherit warning))))
  :bind (:map vc-prefix-map
         ("t" . git-timemachine)))
#+end_src

To resolve the diff conflicts, I use the =smerge-mode= and =ediff= package as my main workflow.
=ediff= is still not visualize things in the way I want, so I am still looking for an alternative way, but this works just fine right now.
#+begin_src emacs-lisp :tangle yes
(use-package smerge-mode
  :ensure nil
  :diminish
  ;; :pretty-hydra
  ;; ((:title (pretty-hydra-title "Smerge" 'octicon "diff")
  ;;   :color pink :quit-key "q")
  ;;  ("Move"
  ;;   (("n" smerge-next "next")
  ;;    ("p" smerge-prev "previous"))
  ;;   "Keep"
  ;;   (("b" smerge-keep-base "base")
  ;;    ("u" smerge-keep-upper "upper")
  ;;    ("l" smerge-keep-lower "lower")
  ;;    ("a" smerge-keep-all "all")
  ;;    ("RET" smerge-keep-current "current")
  ;;    ("C-m" smerge-keep-current "current"))
  ;;   "Diff"
  ;;   (("<" smerge-diff-base-upper "upper/base")
  ;;    ("=" smerge-diff-upper-lower "upper/lower")
  ;;    (">" smerge-diff-base-lower "upper/lower")
  ;;    ("R" smerge-refine "refine")
  ;;    ("E" smerge-ediff "ediff"))
  ;;   "Other"
  ;;   (("C" smerge-combine-with-next "combine")
  ;;    ("r" smerge-resolve "resolve")
  ;;    ("k" smerge-kill-current "kill")
  ;;    ("ZZ" (lambda ()
  ;;            (interactive)
  ;;            (save-buffer)
  ;;            (bury-buffer)
  ;;          )
  ;;     "Save and bury buffer" :exit t))
  ;;  )
  ;; )
  :bind (:map smerge-mode-map
         ("C-c m" . smerge-mode-hydra/body))
  :hook ((find-file . (lambda ()
                        (save-excursion
                          (goto-char (point-min))
                          (when (re-search-forward "^<<<<<<< " nil t)
                            (smerge-mode 1)))))
         (magit-diff-visit-file . (lambda ()
                                    (when smerge-mode
                                      (hydra-smerge/body))))))

(use-package ediff)
(defun ediff-save-window-configuration ()
  "Automatic save window configuration after ediff sessions."
  (window-configuration-to-register ?E))
(defun ediff-restore-window-configuration ()
  "Automatic restore window configuration after ediff sessions."
  (jump-to-register ?E))

(setq-default ediff-before-setup-hook (quote (ediff-save-window-configuration)))
(setq-default ediff-quit-hook (quote (ediff-cleanup-mess ediff-restore-window-configuration exit-recursive-edit)))
(setq-default ediff-suspend-hook (quote (ediff-default-suspend-function ediff-restore-window-configuration)))
(setq-default ediff-window-setup-function (quote ediff-setup-windows-plain))
(setq-default ediff-split-window-function (quote split-window-horizontally))
(setq-default ediff-highlight-all-diffs t)
(setq-default ediff-forward-word-function 'forward-char)

(use-package gitattributes-mode)
(use-package gitconfig-mode)
(use-package gitignore-mode)
#+end_src

** Ibuffer
Changing buffer never feel better than before.
#+begin_src emacs-lisp :tangle yes
(use-package ibuffer
  :ensure nil
  :functions (all-the-icons-icon-for-file
              all-the-icons-icon-for-mode
              all-the-icons-auto-mode-match?
              all-the-icons-faicon
              my-ibuffer-find-file)
  :commands (ibuffer-find-file
             ibuffer-current-buffer)
  :bind ("C-x C-b" . ibuffer)
  :init (setq ibuffer-filter-group-name-face '(:inherit (font-lock-string-face bold)))
  :config
  (setq ibuffer-saved-filter-groups
        '(("Main"
           ("Directories" (mode . dired-mode))
           ("Org" (mode . org-mode))
           ("Programming" (or
                           (mode . c-mode)
                           (mode . conf-mode)
                           (mode . css-mode)
                           (mode . emacs-lisp-mode)
                           (mode . html-mode)
                           (mode . mhtml-mode)
                           (mode . python-mode)
                           (mode . ruby-mode)
                           (mode . scss-mode)
                           (mode . shell-script-mode)
                           (mode . yaml-mode)))
           ("Markdown" (mode . markdown-mode))
           ("Magit" (or
                     (mode . magit-blame-mode)
                     (mode . magit-cherry-mode)
                     (mode . magit-diff-mode)
                     (mode . magit-log-mode)
                     (mode . magit-process-mode)
                     (mode . magit-status-mode)))
           ("Apps" (or
                    (mode . bongo-playlist-mode)
                    (mode . mu4e-compose-mode)
                    (mode . mu4e-headers-mode)
                    (mode . mu4e-main-mode)
                    (mode . elfeed-search-mode)
                    (mode . elfeed-show-mode)
                    (mode . mu4e-view-mode)))
           ("Emacs" (or
                     (name . "^\\*Help\\*$")
                     (name . "^\\*Custom.*")
                     (name . "^\\*Org Agenda\\*$")
                     (name . "^\\*info\\*$")
                     (name . "^\\*scratch\\*$")
                     (name . "^\\*Backtrace\\*$")
                     (name . "^\\*Messages\\*$"))))))
  ;; Display buffer icons on GUI
  (when (and (display-graphic-p)
             (require 'all-the-icons nil t))
    ;; For alignment, the size of the name field should be the width of an icon
    (define-ibuffer-column icon (:name "  ")
      (let ((icon (if (and (buffer-file-name)
                           (all-the-icons-auto-mode-match?))
                      (all-the-icons-icon-for-file (file-name-nondirectory (buffer-file-name)) :v-adjust -0.05)
                    (all-the-icons-icon-for-mode major-mode :v-adjust -0.05))))
        (if (symbolp icon)
            (setq icon (all-the-icons-faicon "file-o" :face 'all-the-icons-dsilver :height 0.8 :v-adjust 0.0))
          icon)))
    (setq ibuffer-formats `((mark modified read-only ,(if emacs/>=26p 'locked "")
                                  ;; Here you may adjust by replacing :right with :center or :left
                                  ;; According to taste, if you want the icon further from the name
                                  " " (icon 2 2 :left :elide)
                                  ,(propertize " " 'display `(space :align-to 8))
                                  (name 18 18 :left :elide)
                                  " " (size 9 -1 :right)
                                  " " (mode 16 16 :left :elide) " " filename-and-process)
                            (mark " " (name 16 -1) " " filename))))
  (with-eval-after-load 'counsel
    (defun my-ibuffer-find-file ()
      (interactive)
      (let ((default-directory (let ((buf (ibuffer-current-buffer)))
                                 (if (buffer-live-p buf)
                                     (with-current-buffer buf
                                       default-directory)
                                   default-directory))))
        (counsel-find-file default-directory)))
    (advice-add #'ibuffer-find-file :override #'my-ibuffer-find-file))
  ;; Group ibuffer's list by project root
  (use-package ibuffer-projectile
    :functions all-the-icons-octicon ibuffer-do-sort-by-alphabetic
    :hook ((ibuffer . (lambda ()
                        (ibuffer-projectile-set-filter-groups)
                        (unless (eq ibuffer-sorting-mode 'alphabetic)
                          (ibuffer-do-sort-by-alphabetic)))))
    :config
    (setq ibuffer-projectile-prefix
          (if (display-graphic-p)
              (concat
               (all-the-icons-octicon "file-directory"
                                      :face ibuffer-filter-group-name-face
                                      :v-adjust -0.05
                                      :height 1.25)
               " ")
            "Project: ")))
  :hook
  (ibuffer-mode . (lambda ()
                    (ibuffer-switch-to-saved-filter-groups "Main"))))
(use-package ibuffer-vc
  :defer t
  :ensure t
  :config
  (define-ibuffer-column icon
    (:name "Icon" :inline t)
    (all-the-icons-ivy--icon-for-mode major-mode))
  :custom
  (ibuffer-formats
   '((mark modified read-only vc-status-mini " "
           (name 18 18 :left :elide)
           " "
           (size 9 -1 :right)
           " "
           (mode 16 16 :left :elide)
           " "
           filename-and-process)) "include vc status info")
  :hook
  (ibuffer . (lambda ()
               (ibuffer-vc-set-filter-groups-by-vc-root)
               (unless (eq ibuffer-sorting-mode 'alphabetic)
                 (ibuffer-do-sort-by-alphabetic)
               )
             )))
#+end_src

** Ivy
Between Helm and Ivy, I always prefer the compact UI of Ivy and it run smoother than Helm for me.
=flx= is a package support for this configuration, which should be there in the first place.
#+begin_src emacs-lisp :tangle yes
(use-package flx)
#+end_src

*Ivy* can be easily setup with these lines:
#+begin_src emacs-lisp :tangle yes
(use-package ivy
  :diminish
  :hook (after-init . ivy-mode)
  :config
  (setq ivy-display-style nil)
  (define-key ivy-minibuffer-map (kbd "RET") #'ivy-alt-done)
  (define-key ivy-minibuffer-map (kbd "<escape>") #'minibuffer-keyboard-quit)
  (setq ivy-re-builders-alist
        '((counsel-rg . ivy--regex-plus)
          (counsel-projectile-rg . ivy--regex-plus)
          (counsel-ag . ivy--regex-plus)
          (counsel-projectile-ag . ivy--regex-plus)
          (swiper . ivy--regex-plus)
          (t . ivy--regex-fuzzy)))
  (setq ivy-use-virtual-buffers t
        ivy-count-format "(%d/%d) "
        ivy-initial-inputs-alist nil))
#+end_src

=ivy-rich= is an enhancement for Ivy, which show command description and other info.
#+begin_src emacs-lisp :tangle yes
(use-package ivy-rich
  :init
  (setq ivy-rich-display-transformers-list ; max column width sum = (ivy-poframe-width - 1)
        '(ivy-switch-buffer
          (:columns
           ((ivy-rich-candidate (:width 35))
            (ivy-rich-switch-buffer-project (:width 15 :face success))
            (ivy-rich-switch-buffer-major-mode (:width 13 :face warning)))
           :predicate
           #'(lambda (cand) (get-buffer cand))
          )
          counsel-M-x
          (:columns
           ((counsel-M-x-transformer (:width 40))
            (ivy-rich-counsel-function-docstring (:width 40 :face font-lock-doc-face))
           )
          )
          counsel-describe-function
          (:columns
           ((counsel-describe-function-transformer (:width 35))
            (ivy-rich-counsel-function-docstring (:width 34 :face font-lock-doc-face))))
          counsel-describe-variable
          (:columns
           ((counsel-describe-variable-transformer (:width 35))
            (ivy-rich-counsel-variable-docstring (:width 34 :face font-lock-doc-face))))
          package-install
          (:columns
           ((ivy-rich-candidate (:width 25))
            (ivy-rich-package-version (:width 12 :face font-lock-comment-face))
            (ivy-rich-package-archive-summary (:width 7 :face font-lock-builtin-face))
            (ivy-rich-package-install-summary (:width 23 :face font-lock-doc-face))))))
  :hook (after-init . ivy-rich-mode)
  :config
  (setcdr (assq t ivy-format-functions-alist) #'ivy-format-function-line))
#+end_src

Since I found pleasure working with minibuffer, sometime it can be a new fresh to use postframe.
#+begin_src emacs-lisp :tangle yes
;; (use-package ivy-posframe
;;   :after ivy
;;   :diminish
;;   :config
;;   (setq ivy-posframe-display-functions-alist '((t . ivy-posframe-display))
;;         ivy-posframe-height-alist '((t . 20))
;;         ivy-posframe-parameters '((internal-border-width . 10)))
;;   (setq ivy-posframe-width 100))
;; (ivy-posframe-mode 1)
#+end_src

** Hydra
Hydra is a superior package, it helps me to speed thing up when it come to hotkey for a specific task.
Though I love the way =hydra= let me draw my ascii in the minibuffer, =pretty-hydra= has done a better job at visualizing.
#+begin_src emacs-lisp :tangle yes
(use-package hydra
  :bind
  ("C-c h a" . hydra-drawibm/body)
  :custom
  (hydra-default-hint nil))

(use-package pretty-hydra
  :bind
  ("C-c h l" . hydra-lspui/body)
  ("C-c h f" . hydra-flycheck/body)
  ("C-c h p" . hydra-projectile/body)
  ("C-c h w" . hydra-window/body))
#+end_src

For =pretty-hydra=, we need a custom function for it to add the title and icon with color
#+begin_src emacs-lisp :tangle yes
(defun phydra-title (title &optional icon-type icon-name
                           &key face height v-adjust)
      "Add an icon in the hydra title."
      (let ((face (or face `(:foreground ,(face-background 'highlight))))
            (height (or height 1.0))
            (v-adjust (or v-adjust 0.0)))
        (concat
         (when (and (display-graphic-p) icon-type icon-name)
           (let ((f (intern (format "all-the-icons-%s" icon-type))))
             (when (fboundp f)
               (concat
                (apply f (list icon-name :face face :height height :v-adjust v-adjust))
                " "))))
         (propertize title 'face face))))
#+end_src

*DASHBOARD*
#+begin_src emacs-lisp :tangle yes
(pretty-hydra-define hydra-dashboard (:title (phydra-title "Dashboard" 'material "dashboard")
                                      :color pink
                                      :quit-key "q"
                                     )
  ("Navigator"
   (("U" update-config-and-packages "update" :exit t)
    ("H" browse-homepage "homepage" :exit t)
    ("R" restore-session "recover session" :exit t)
    ("L" persp-load-state-from-file "list sessions" :exit t)
    ("S" open-custom-file "settings" :exit t))

   "Section"
   (("}" dashboard-next-section "next")
    ("{" dashboard-previous-section "previous")
    ("r" dashboard-goto-recent-files "recent files")
    ("m" dashboard-goto-bookmarks "projects")
    ("p" dashboard-goto-projects "bookmarks"))

   "Item"
   (("RET" widget-button-press "open" :exit t)
    ("<tab>" widget-forward "next")
    ("C-i" widget-forward "next")
    ("<backtab>" widget-backward "previous")
    ("C-n" next-line "next line")
    ("C-p" previous-line "previous  line"))

   "Misc"
   (("<f2>" open-dashboard "open" :exit t)
    ("g" dashboard-refresh-buffer "refresh" :exit t)
    ("Q" quit-dashboard "quit" :exit t))))
#+end_src

*IBM DRAWING*
#+begin_src emacs-lisp :tangle yes
(defcustom penguin/ibm-overwrite nil "Overwrite mode for IBM (codepage 437) box drawing.")
(defun penguin/ibm-insert (char)
  "Insert CHAR with conditional overwrite."
  (interactive)
  (when penguin/ibm-overwrite
    (kill-char 1))
  (insert char))

(defhydra hydra-drawibm (:color pink)
  "
IBM Box Chars  _r_ ‚îÄ         _R_ ‚ïê         _v_ ‚îÇ         _V_ ‚ïë
(CodePage 437) _q_ ‚îå _w_ ‚î¨ _e_ ‚îê _Q_ ‚ïí _W_ ‚ï§ _E_ ‚ïï _t_ ‚ïî _y_ ‚ï¶ _u_ ‚ïó _T_ ‚ïì _Y_ ‚ï• _U_ ‚ïñ  _C-q_ ‚ï≠ ‚ïÆ _C-w_
               _a_ ‚îú _s_ ‚îº _d_ ‚î§ _A_ ‚ïû _S_ ‚ï™ _D_ ‚ï° _g_ ‚ï† _h_ ‚ï¨ _j_ ‚ï£ _G_ ‚ïü _H_ ‚ï´ _J_ ‚ï¢
               _z_ ‚îî _x_ ‚î¥ _c_ ‚îò _Z_ ‚ïò _X_ ‚ïß _C_ ‚ïõ _b_ ‚ïö _n_ ‚ï© _m_ ‚ïù _B_ ‚ïô _N_ ‚ï® _M_ ‚ïú  _C-a_ ‚ï∞ ‚ïØ _C-s_
_ESC_ to exit    _i_ Toggle Overwrite/Insert
"
   ("ESC" nil nil :color blue)
   ("<space>"   (search-backward "+"))
   ("S-<space>" (search-forward "+"))
   ("q" (penguin/ibm-insert "‚îå")) ("w" (penguin/ibm-insert "‚î¨")) ("e" (penguin/ibm-insert "‚îê"))
   ("Q" (penguin/ibm-insert "‚ïí")) ("W" (penguin/ibm-insert "‚ï§")) ("E" (penguin/ibm-insert "‚ïï"))
   ("t" (penguin/ibm-insert "‚ïî")) ("y" (penguin/ibm-insert "‚ï¶")) ("u" (penguin/ibm-insert "‚ïó"))
   ("T" (penguin/ibm-insert "‚ïì")) ("Y" (penguin/ibm-insert "‚ï•")) ("U" (penguin/ibm-insert "‚ïñ"))
   ("a" (penguin/ibm-insert "‚îú")) ("s" (penguin/ibm-insert "‚îº")) ("d" (penguin/ibm-insert "‚î§"))
   ("A" (penguin/ibm-insert "‚ïû")) ("S" (penguin/ibm-insert "‚ï™")) ("D" (penguin/ibm-insert "‚ï°"))
   ("g" (penguin/ibm-insert "‚ï†")) ("h" (penguin/ibm-insert "‚ï¨")) ("j" (penguin/ibm-insert "‚ï£"))
   ("G" (penguin/ibm-insert "‚ïü")) ("H" (penguin/ibm-insert "‚ï´")) ("J" (penguin/ibm-insert "‚ï¢"))
   ("z" (penguin/ibm-insert "‚îî")) ("x" (penguin/ibm-insert "‚î¥")) ("c" (penguin/ibm-insert "‚îò"))
   ("Z" (penguin/ibm-insert "‚ïò")) ("X" (penguin/ibm-insert "‚ïß")) ("C" (penguin/ibm-insert "‚ïõ"))
   ("b" (penguin/ibm-insert "‚ïö")) ("n" (penguin/ibm-insert "‚ï©")) ("m" (penguin/ibm-insert "‚ïù"))
   ("B" (penguin/ibm-insert "‚ïô")) ("N" (penguin/ibm-insert "‚ï®")) ("M" (penguin/ibm-insert "‚ïú"))
   ("r" (penguin/ibm-insert "‚îÄ")) ("R" (penguin/ibm-insert "‚ïê"))
   ("v" (penguin/ibm-insert "‚îÇ")) ("V" (penguin/ibm-insert "‚ïë"))
   ("C-q" (penguin/ibm-insert "‚ï≠")) ("C-w" (penguin/ibm-insert "‚ïÆ"))
   ("C-a" (penguin/ibm-insert "‚ï∞")) ("C-s" (penguin/ibm-insert "‚ïØ"))
   ("i" (setq penguin/ibm-overwrite (not penguin/ibm-overwrite)) ))
#+end_src

*IVY*
TODO

*FLYCHECK*
#+begin_src emacs-lisp :tangle yes
(pretty-hydra-define hydra-flycheck (:title (phydra-title "Flycheck" 'faicon "bug")
                                     :color pink
                                     :quit-key "q")
  ("Documentation"
   (("m" flycheck-manual "manual" :exit t)
    ("v" flycheck-verify-setup "verify setup" :exit t))
   "Errors"
   ((">" flycheck-next-error "next")
    ("<" flycheck-previous-error "previous")
    ("f" flycheck-buffer "check")
    ("l" flycheck-list-errors "list"))
   "Checker"
   (("?" flycheck-describe-checker "describe")
    ("d" flycheck-disable-checker "disable")
    ("s" flycheck-select-checker "select"))))
#+end_src

*LSP-UI*
#+begin_src emacs-lisp :tangle yes
(defun penguin/toggle-lsp-ui-doc ()
  "Toggle the LSP UI."
  (interactive)
  (if lsp-ui-doc-mode
    (progn
      (lsp-ui-doc-mode -1)
      (lsp-ui-doc--hide-frame))
    (lsp-ui-doc-mode 1)))

(pretty-hydra-define hydra-lspui (:title (phydra-title "LSP UI" 'faicon "rocket")
                                  :color amaranth
                                  :quit-key "q")
   ("Doc"
    (("d e" penguin/toggle-lsp-ui-doc "enable" :toggle t)
     ("d s" lsp-ui-doc-include-signature "signature" :toggle t)
     ("d t" (setq lsp-ui-doc-position 'top) "top" :toggle (eq lsp-ui-doc-position 'top))
     ("d b" (setq lsp-ui-doc-position 'bottom) "bottom" :toggle (eq lsp-ui-doc-position 'bottom))
     ("d p" (setq lsp-ui-doc-position 'at-point) "at point" :toggle (eq lsp-ui-doc-position 'at-point)))
    "Sideline"
    (("s e" lsp-ui-sideline-enable "enable" :toggle t)
     ("s h" lsp-ui-sideline-show-hover "hover" :toggle t)
     ("s d" lsp-ui-sideline-show-diagnostics "diagnostics" :toggle t)
     ("s s" lsp-ui-sideline-show-symbol "symbol" :toggle t)
     ("s c" lsp-ui-sideline-show-code-actions "code actions" :toggle t)
     ("s i" lsp-ui-sideline-ignore-duplicate "ignore duplicate" :toggle t))))
#+end_src

*MAGIT*
TODO

*MARKDOWN*
TODO

*ORG*
TODO

*PROJECTILE*
#+begin_src emacs-lisp :tangle yes
(pretty-hydra-define hydra-projectile (:title (phydra-title "Projectile" 'faicon "rocket")
                                       :color pink
                                       :quit-key "q"
                                      )
  ("Buffers"
   (("b" counsel-projectile-switch-to-buffer "list")
    ("k" projectile-kill-buffers "kill all")
    ("S" projectile-save-project-buffers "save all"))
   "Find"
   (("d" counsel-projectile-find-dir "directory")
    ("D" projectile-dired "root")
    ("f" counsel-projectile-find-file "file")
    ("p" counsel-projectile-switch-project "project"))
   "Other"
   (("i" projectile-invalidate-cache "reset cache"))
   "Search"
   (("r" projectile-replace "replace")
    ("R" projectile-replace-regexp "regexp replace")
    ("s" counsel-rg "search"))))
#+end_src

*WINDOW*
#+begin_src emacs-lisp :tangle yes
(use-package ace-window)
(pretty-hydra-define hydra-window (:foreign-keys warn
                                   :title (phydra-title "Windows Management" 'faicon "windows")
                                   :quit-key "q"
                                   :color amaranth
                                  )
  ("Actions"
   (("TAB" other-window "switch")
    ("x" ace-delete-window "delete")
    ("m" ace-delete-other-windows "maximize")
    ("s" ace-swap-window "swap")
    ("a" ace-select-window "select"))

   "Resize"
   (("i" enlarge-window "‚Üë up")
    ("k" shrink-window "‚Üì down")
    ("j" shrink-window-horizontally "‚Üê left")
    ("l" enlarge-window-horizontally "‚Üí right")
    ("n" balance-windows "balance")
    ("f" toggle-frame-fullscreen "toggle fullscreen"))

   "Select"
   (("p" windmove-up "‚Üë up")
    ("n" windmove-down "‚Üì down")
    ("b" windmove-left "‚Üê left")
    ("f" windmove-right "‚Üí right"))

   "Split"
   (("h" (lambda ()
           (interactive)
           (split-window-below)
           (windmove-down)
         ) "horizontally")
    ("v" (lambda ()
           (interactive)
           (split-window-right)
           (windmove-right)
         ) "vertically"))))
#+end_src

** Paren
Automatic paren
#+begin_src emacs-lisp :tangle yes
(use-package elec-pair
  :ensure nil
  :hook (after-init . electric-pair-mode)
  :init (setq electric-pair-inhibit-predicate 'electric-pair-conservative-inhibit))
#+end_src

** PDF Reader
Reading is never a worthless hobby. Since Emacs is my main workspace, I would prefer to read and write on it at the same time.
The usual format for any book I can find is PDF. The =pdf-view= package is required for this job.
#+begin_src emacs-lisp :tangle yes
(when (display-graphic-p)
  (use-package pdf-view
    :ensure pdf-tools
    :diminish (pdf-view-midnight-minor-mode pdf-view-printer-minor-mode)
    :defines pdf-annot-activate-created-annotations
    :functions (penguin/pdf-view-set-midnight-colors penguin/pdf-view-set-dark-theme)
    :commands pdf-view-midnight-minor-mode
    :mode ("\\.[pP][dD][fF]\\'" . pdf-view-mode)
    :magic ("%PDF" . pdf-view-mode)
    :bind (:map pdf-view-mode-map
           ("C-s" . isearch-forward))
    :init (setq pdf-annot-activate-created-annotations t)
    :config
    ;; WORKAROUND: Fix compilation errors on macOS.
    ;; @see https://github.com/politza/pdf-tools/issues/480
    (when sys/macos
      (setenv "PKG_CONFIG_PATH"
              "/usr/local/lib/pkgconfig:/usr/local/opt/libffi/lib/pkgconfig"))
    (pdf-tools-install t nil t t)

    ;; Set dark theme
    (defun penguin/pdf-view-set-midnight-colors ()
      "Set pdf-view midnight colors."
      (setq pdf-view-midnight-colors
            `(,(face-foreground 'default) . ,(face-background 'default))))

    (defun penguin/pdf-view-set-dark-theme ()
      "Set pdf-view midnight theme as color theme."
      (penguin/pdf-view-set-midnight-colors)
      (dolist (buf (buffer-list))
        (with-current-buffer buf
          (when (eq major-mode 'pdf-view-mode)
            (pdf-view-midnight-minor-mode (if pdf-view-midnight-minor-mode 1 -1))))))

    (penguin/pdf-view-set-midnight-colors)
    (add-hook 'after-load-theme-hook #'penguin/pdf-view-set-dark-theme)

    ;; FIXME: Support retina
    ;; @see https://emacs-china.org/t/pdf-tools-mac-retina-display/10243/
    ;; and https://github.com/politza/pdf-tools/pull/501/
    (setq pdf-view-use-scaling t
          pdf-view-use-imagemagick nil)
    (with-no-warnings
      (defun pdf-view-use-scaling-p ()
        "Return t if scaling should be used."
        (and (or (and (eq system-type 'darwin) (>= emacs-major-version 27))
                 (memq (pdf-view-image-type) '(imagemagick image-io)))
             pdf-view-use-scaling))
      (defun pdf-view-create-page (page &optional window)
        "Create an image of PAGE for display on WINDOW."
        (let* ((size (pdf-view-desired-image-size page window))
               (width (if (not (pdf-view-use-scaling-p))
                          (car size)
                        (* 2 (car size))))
               (data (pdf-cache-renderpage
                      page width width))
               (hotspots (pdf-view-apply-hotspot-functions
                          window page size)))
          (pdf-view-create-image data
            :width width
            :scale (if (pdf-view-use-scaling-p) 0.5 1)
            :map hotspots
            :pointer 'arrow))))
    ;; Recover last viewed position
    (when emacs/>=26p
      (use-package pdf-view-restore
        :hook (pdf-view-mode . pdf-view-restore-mode)
        :init (setq pdf-view-restore-filename
                    (locate-user-emacs-file ".pdf-view-restore"))))))
#+end_src

Epub format is supported, too.
#+begin_src emacs-lisp :tangle yes
;; Epub reader
(use-package nov
  :mode ("\\.epub\\'" . nov-mode)
  :functions centaur-read-mode
  :hook (nov-mode . penguin/nov-setup)
  :init
  (defun penguin/nov-setup ()
    "Setup `nov-mode' for better reading experience."
    (visual-line-mode 1)
    (centaur-read-mode)
    (face-remap-add-relative 'variable-pitch :family "Times New Roman" :height 1.5))
  :config
  ;; FIXME: errors while opening `nov' files with Unicode characters
  ;; @see https://github.com/wasamasa/nov.el/issues/63
  (with-no-warnings
    (defun penguin/nov-content-unique-identifier (content)
      "Return the the unique identifier for CONTENT."
      (when-let* ((name (nov-content-unique-identifier-name content))
                  (selector (format "package>metadata>identifier[id='%s']"
                                    (regexp-quote name)))
                  (id (car (esxml-node-children (esxml-query selector content)))))
        (intern id)))
    (advice-add #'nov-content-unique-identifier :override #'penguin/nov-content-unique-identifier))
  ;; Fix encoding issue on Windows
  (when sys/win32
    (setq process-coding-system-alist
          (cons `(,nov-unzip-program . (gbk . gbk))
                process-coding-system-alist))))
#+end_src

** Projectile
Manage file with projects is a must. Projectile is a powerful enhancement for Emacs to handle these jobs.
Mostly I will prefer ivy-counsel stuff but working with helm-projectile cause me an unknown pleasure.
#+begin_src emacs-lisp :tangle yes
(setq projectile-known-projects-file (concat pcache-dir "projectile.projects"))

(use-package projectile
  :diminish
  :bind (:map projectile-mode-map
         ("s-t" . projectile-find-file) ; `cmd-t' or `super-t'
         ("C-c p" . projectile-command-map))
  :hook (after-init . projectile-mode)
  :init
  (setq projectile-mode-line-prefix " "
        projectile-sort-order 'recentf
        projectile-use-git-grep t)
  :config
  ;; Helm support
  (projectile-mode)
  (use-package helm-projectile
    :ensure t)
  (setq projectile-completion-system 'helm)
  (setq helm-default-display-buffer-functions '(display-buffer-in-side-window))
  (helm-projectile-on)
  (use-package wgrep
    :ensure t
    :config
    (use-package wgrep-helm :ensure t)
    (setq wgrep-auto-save-buffer t)
    (setq wgrep-change-readonly-file t))
  ;; Use the faster searcher to handle project files: ripgrep `rg'.
  (when (and (not (executable-find "fd"))
             (executable-find "rg"))
    (setq projectile-generic-command
          (let ((rg-cmd ""))
            (dolist (dir projectile-globally-ignored-directories)
              (setq rg-cmd (format "%s --glob '!%s'" rg-cmd dir)))
            (concat "rg -0 --files --color=never --hidden" rg-cmd))))
  ;; Faster searching on Windows
  (when sys/win32
    (setq projectile-indexing-method 'alien)
    (when (or (executable-find "fd") (executable-find "rg"))
      (setq projectile-indexing-method 'alien
            projectile-enable-caching nil))
    (setq projectile-git-submodule-command nil))
  ;; Support Perforce project
  (let ((val (or (getenv "P4CONFIG") ".p4config")))
    (add-to-list 'projectile-project-root-files-bottom-up val)))
#+end_src

Let's define some file that projectile should need to ignore to increase performance and the workspace seem more clean.
#+begin_src emacs-lisp :tangle yes
(setq projectile-cache-file (concat pcache-dir "projectile.cache")
      projectile-enable-caching (not noninteractive)
      projectile-indexing-method 'alien
      projectile-known-projects-file (concat pcache-dir "projectile.projects")
      projectile-require-project-root nil
      projectile-globally-ignored-files '(".DS_Store" "Icon" "TAGS")
      projectile-globally-ignored-file-suffixes '(".elc" ".pyc" ".o"))
#+end_src

** Swiper
A better search for Emacs
#+begin_src emacs-lisp :tangle yes
(use-package swiper
  :after ivy
  :preface
  (defun penguin/swiper ()
    "`swiper' with string returned by `ivy-thing-at-point' as initial input."
    (interactive)
    (swiper (ivy-thing-at-point)))
  :bind (("C-s" . penguin/swiper)
         :map swiper-map
         ("C-r" . swiper-query-replace))
  :config
  (setq swiper-action-recenter t)
  (setq swiper-goto-start-of-match t))
(use-package anzu
  :bind
  ([remap query-replace] . anzu-qxuery-replace-regexp)
  :hook
  (after-init . global-anzu-mode)
  :custom
  (anzu-cons-mode-line-p nil))
#+end_src

I also need to enhance the experience with M-x itself, helm is good for that but I find counsel and ivy better in performance.
#+begin_src emacs-lisp :tangle yes
(use-package counsel
  :diminish
  :hook (ivy-mode . counsel-mode)
  :bind (("C-S-s" . helm-projectile-grep))
  :custom
  (counsel-rg-base-command "rg --vimgrep %s")
  :config
  (setq counsel-yank-pop-preselect-last t)
  (setq counsel-yank-pop-separator "\n‚Äî‚Äî‚Äî‚Äî‚Äî‚Äî‚Äî‚Äî‚Äî\n")
  (setq counsel-rg-base-command
        "rg -SHn --no-heading --color never --no-follow --hidden %s"))

(use-package counsel-projectile
  :after ivy
  :config
  (setq-default ivy-initial-inputs-alist nil))
(counsel-projectile-mode 1)
#+end_src

** Terminal
This config is quite long but simple. If you are on Windows, use shell (built-in Emacs) else use vterm
#+begin_src emacs-lisp :tangle yes
(use-package shell
  :ensure nil
  :commands comint-send-string comint-simple-send comint-strip-ctrl-m
  :hook ((shell-mode . ansi-color-for-comint-mode-on)
         (shell-mode . n-shell-mode-hook)
         (comint-output-filter-functions . comint-strip-ctrl-m))
  :init
  (setq system-uses-terminfo nil)
  (defun n-shell-simple-send (proc command)
    "Various PROC COMMANDs pre-processing before sending to shell."
    (cond
     ;; Checking for clear command and execute it.
     ((string-match "^[ \t]*clear[ \t]*$" command)
      (comint-send-string proc "\n")
      (erase-buffer))
     ;; Checking for man command and execute it.
     ((string-match "^[ \t]*man[ \t]*" command)
      (comint-send-string proc "\n")
      (setq command (replace-regexp-in-string "^[ \t]*man[ \t]*" "" command))
      (setq command (replace-regexp-in-string "[ \t]+$" "" command))
      ;;(message (format "command %s command" command))
      (funcall 'man command))
     ;; Send other commands to the default handler.
     (t (comint-simple-send proc command))))
  (defun n-shell-mode-hook ()
    "Shell mode customizations."
    (local-set-key '[up] 'comint-previous-input)
    (local-set-key '[down] 'comint-next-input)
    (local-set-key '[(shift tab)] 'comint-next-matching-input-from-input)
    (setq comint-input-sender 'n-shell-simple-send)))

;; Shell Pop
(use-package shell-pop
  :bind ([f9] . shell-pop)
  :init
  (setq shell-pop-window-size 35
        shell-pop-shell-type
        (cond (sys/win32 '("eshell" "*eshell*" (lambda () (eshell))))
              ((fboundp 'vterm) '("vterm" "*vterm*" (lambda () (vterm))))
              (t '("terminal" "*terminal*" (lambda () (term shell-pop-term-shell)))))))
#+end_src

To make sure Emacs would run vterm smoothly:
#+begin_src emacs-lisp :tangle yes
;; Better term
;; @see https://github.com/akermu/emacs-libvterm#installation
(when (and module-file-suffix           ; dynamic module
           (executable-find "cmake")
           (executable-find "libtool")
           (executable-find "make"))
  (use-package vterm))
#+end_src

While using custom zsh, I prefer to get zsh highlight to make the terminal look better. This package will get ANSI & XTERM 256 color support.
#+begin_src emacs-lisp :tangle yes
(use-package xterm-color
  :defines (compilation-environment
            eshell-preoutput-filter-functions
            eshell-output-filter-functions)
  :functions (compilation-filter my-advice-compilation-filter)
  :init
  ;; For shell
  (setenv "TERM" "xterm-256color")
  (setq comint-output-filter-functions
        (remove 'ansi-color-process-output comint-output-filter-functions))
  (add-hook 'shell-mode-hook
            (lambda ()
              ;; Disable font-locking in this buffer to improve performance
              (font-lock-mode -1)
              ;; Prevent font-locking from being re-enabled in this buffer
              (make-local-variable 'font-lock-function)
              (setq font-lock-function (lambda (_) nil))
              (add-hook 'comint-preoutput-filter-functions 'xterm-color-filter nil t)))
  ;; For eshell
  (with-eval-after-load 'esh-mode
    (add-hook 'eshell-before-prompt-hook
              (lambda ()
                (setq xterm-color-preserve-properties t)))
    (add-to-list 'eshell-preoutput-filter-functions 'xterm-color-filter)
    (setq eshell-output-filter-functions
          (remove 'eshell-handle-ansi-color eshell-output-filter-functions)))
  ;; For compilation buffers
  (setq compilation-environment '("TERM=xterm-256color"))
  (defun my-advice-compilation-filter (f proc string)
    (funcall f proc
             (if (eq major-mode 'rg-mode) ; compatible with `rg'
                 string
               (xterm-color-filter string))))
  (advice-add 'compilation-filter :around #'my-advice-compilation-filter)
  (advice-add 'gud-filter :around #'my-advice-compilation-filter)
  ;; For prolog inferior
  (with-eval-after-load 'prolog
    (add-hook 'prolog-inferior-mode-hook
              (lambda ()
                (add-hook 'comint-preoutput-filter-functions 'xterm-color-filter nil t)))))

#+end_src

** Utilities
MacOS path seems broken when yank a url from shell, let's fix that error
#+begin_src emacs-lisp :tangle yes
(when sys/macos
  (use-package exec-path-from-shell
    :ensure t
    :demand t
    :init
    (setq exec-path-from-shell-check-startup-files nil)
    :config
    ;; (exec-path-from-shell-copy-env "PYTHONPATH")
    (when (memq window-system '(mac ns x))
      (exec-path-from-shell-initialize))))
#+end_src

I config my Emacs a lot, so it is helpful if I can restart Emacs quickly.
#+begin_src emacs-lisp :tangle yes
(use-package restart-emacs
  :ensure t
  :bind* (("C-x M-c" . restart-emacs)))
#+end_src

This functions is stayed here for reference purpose. Nothing in use right now.
#+begin_src emacs-lisp :tangle yes
  (defun penguin-emacs-reload-init-file ()
    "Reload your init.el file without restarting Emacs."
    (interactive)
    (load-file "~/.emacs.d/init.el"))

  (defun penguin-emacs-update-config ()
    "Update Penguin Emacs configuration to the latest version."
    (interactive)
    (let ((dir (expand-file-name user-emacs-directory)))
      (progn
        (message "Updating Penguin Emacs configuration...")
        (cd dir)
        (shell-command "git pull")
        (message "Load new Penguin Emacs configuration...")
        (penguin-emacs-reload-init-file)
        (message "Update finished."))))
#+end_src

** Weather Forecast
Like I said before, I rarely turn Emacs off so I just need this to figure out what is going on outside.
#+begin_src emacs-lisp :tangle yes
(use-package wttrin
  :ensure t
  :commands (wttrin)
  :init
  (setq wttrin-default-cities '("Hochiminh")))
#+end_src

** Whitespace
Those whitespace at the end are annoying, let's get rid of them.
#+begin_src emacs-lisp :tangle yes
  (use-package simple
    :ensure nil
    :hook (before-save . delete-trailing-whitespace))
#+end_src


* Language Support
** C++
My main programming language for game development. In order to have a fast and stable environment, I use LSP as a client and LSP Server ccls as sever.
To use =ccls=, we first need to install it with the package manager of our operating system. The instruction can be viewed [[https://github.com/MaskRay/ccls/wiki][here]].
#+begin_src emacs-lisp :tangle yes
  (use-package ccls
    :after projectile
    :ensure t
    :custom
    (ccls-args nil)
    (ccls-executable (executable-find "ccls"))
    (projectile-project-root-files-top-down-recurring
     (append '("compile_commands.json" ".ccls")
             projectile-project-root-files-top-down-recurring))
    :config (push ".ccls-cache" projectile-globally-ignored-directories))

  (use-package google-c-style
    :hook
    ((c-mode c++-mode) . google-set-c-style)
    (c-mode-common . google-make-newline-indent))

  (require 'lsp-clients)
  (setq lsp-disabled-clients '(clangd)) ;; Disable unused clangd server
  ;; (when sys/macos
  ;;   (setq lsp-clients-clangd-executable "/usr/local/opt/llvm/bin/clangd"))
#+end_src

To allow =ccls= to know the dependencies of your =.cpp= files with your =.h= files, it is important to provide an =compile.commands.json= file (or a =.ccls= file) at the root of your project.
For this, nothing could be easier. If like me you use a =CMakeLists.txt= file for all your C++ projects, then you just need to install the cmake package on your operating system and to generate the =compile.commands.json= file, you have to do:
#+begin_src
  cmake -H. -BDebug -DCMAKE_BUILD_TYPE=Debug -DCMAKE_EXPORT_COMPILE_COMMANDS=YES
  ln -s Debug/compile_commands.json
#+end_src

For every files, I'd want to have a steady and clean code throughout the project. So I will use =clang-format= package to stay organized.
#+begin_src emacs-lisp :tangle yes
  (use-package clang-format)
  (defun clang-format-on-save ()
    "Format selected region to clangformat."
    (add-hook 'before-save-hook #'clang-format-buffer nil 'local))
  (add-hook 'c++-mode-hook 'clang-format-on-save)
  (add-hook 'c-mode-hook 'clang-format-on-save)

  ;; Addon - Clang format Google Style on C-f10
  (require 'cc-mode)
  (defun clang-format-region-google (s e)
    "Format the selected region S & E."
    (interactive
     (if (use-region-p)
         (list (region-beginning) (region-end))
       (list (point) (point))))
    (clang-format-region s e "Google"))

  (define-key c-mode-base-map (kbd "C-<f10>") #'clang-format-region-google)
#+end_src

> This is just temperary. =clang-tidy= in CLion is much more helpful. (TODO)

** CMake
My work requires me to go cross-platform with C++. So, =cmake= is a must.
#+begin_src emacs-lisp :tangle yes
  (use-package cmake-mode)
  (setq auto-mode-alist
        (append '(("CMakeLists\\.txt\\'" . cmake-mode))
            '(("\\.cmake\\'" . cmake-mode))
            auto-mode-alist))

  (use-package cmake-font-lock
    :after (cmake-mode)
    :hook (cmake-mode . cmake-font-lock-activate))

  (use-package cmake-ide
    :after projectile
    :hook (c++-mode . penguin/cmake-ide-find-project)
    :preface
    (defun penguin/cmake-ide-find-project ()
      "Finds the directory of the project for cmake-ide."
      (with-eval-after-load 'projectile
        (setq cmake-ide-project-dir (projectile-project-root))
        (setq cmake-ide-build-dir (concat cmake-ide-project-dir "build")))
      (setq cmake-ide-compile-command
            (concat "cd " cmake-ide-build-dir " && cmake .. && make"))
      (cmake-ide-load-db))

    (defun penguin/switch-to-compilation-window ()
      "Switches to the *compilation* buffer after compilation."
      (other-window 1))
    :bind ([remap comment-region] . cmake-ide-compile)
    :init (cmake-ide-setup)
    :config (advice-add 'cmake-ide-compile :after #'penguin/switch-to-compilation-window))
#+end_src

** CSS
All these stuffs is for my habit of working around with themes of other softwares (especially open-source).
#+begin_src emacs-lisp :tangle yes
  (use-package css-mode
    :ensure nil
    :custom (css-indent-offset 2))

  (use-package scss-mode
    :ensure nil
    :preface
    (defun me/scss-set-comment-style ()
      (setq-local comment-end "")
      (setq-local comment-start "//"))
    :mode ("\\.sass\\'" "\\.scss\\'")
    :hook (scss-mode . me/scss-set-comment-style))
#+end_src

** CSV
#+begin_src emacs-lisp :tangle yes
  (use-package csv-mode
    :config (setq-default csv-align-padding 2))
#+end_src

** Elisp
These whole configuration is written in emacs-lisp (Elisp), a programming language specific for developing Emacs. Its only issue for me is the brackets ..
#+begin_src emacs-lisp :tangle yes
  (use-package ielm
    :ensure nil
    :hook (ielm-mode . (lambda () (setq-local scroll-margin 0))))

  (use-package elisp-mode :ensure nil :delight "Œæ ")

  (use-package eldoc
    :delight
    :hook (emacs-lisp-mode . eldoc-mode))
#+end_src

** Json
Json is really helpful most of the time. I'd like to have a nice package config for it.

#+begin_src emacs-lisp :tangle yes
  (use-package json-mode
    :delight "J "
    :mode "\\.json\\'"
    :hook (before-save . penguin/json-mode-before-save-hook)
    :preface
    (defun penguin/json-mode-before-save-hook ()
      (when (eq major-mode 'json-mode)
        (json-pretty-print-buffer)))
    (defun penguin/json-array-of-numbers-on-one-line (encode array)
      "Prints the arrays of numbers in one line."
      (let* ((json-encoding-pretty-print
              (and json-encoding-pretty-print
                   (not (loop for x across array always (numberp x)))))
             (json-encoding-separator (if json-encoding-pretty-print "," ", ")))
        (funcall encode array)))
    :config (advice-add 'json-encode-array :around #'penguin/json-array-of-numbers-on-one-line))
#+end_src

** HTML
In order to have a fast and stable environment, I recommend using **LSP** as a client for LSP servers and [[https://github.com/vscode-langservers/vscode-html-languageserver][vscode-html-languageserver]] as server.

To use =vscode-html-languageserver= with GNU Emacs, you must first install it with the package manager of your operating system.

Let‚Äôs configure =emmet-mode=, to produce HTML from CSS-like selector:
#+BEGIN_SRC emacs-lisp :tangle yes
  (use-package emmet-mode
    :delight
    :hook (css-mode sgml-mode web-mode))
#+END_SRC

** Latex
I'd love to learn LaTex to manage my resume and CV, maybe for some fancy presentation someday. This package config is for the future, perhaps üòâ.
#+begin_src emacs-lisp :tangle yes
  (use-package tex
    :ensure auctex
    :bind (:map TeX-mode-map
                ("C-c C-o" . TeX-recenter-output-buffer)
                ("C-c C-l" . TeX-next-error)
                ("M-[" . outline-previous-heading)
                ("M-]" . outline-next-heading))
    :hook (LaTeX-mode . reftex-mode)
    :preface
    (defun penguin/switch-to-help-window (&optional ARG REPARSE)
      "Switches to the *TeX Help* buffer after compilation."
      (other-window 1))
    :custom
    (TeX-auto-save t)
    (TeX-byte-compile t)
    (TeX-clean-confirm nil)
    (TeX-master 'dwim)
    (TeX-parse-self t)
    (TeX-PDF-mode t)
    (TeX-source-correlate-mode t)
    (TeX-view-program-selection '((output-pdf "PDF Tools")))
    :config
    (advice-add 'TeX-next-error :after #'penguin/switch-to-help-window)
    (advice-add 'TeX-recenter-output-buffer :after #'penguin/switch-to-help-window)
    (add-hook 'TeX-after-compilation-finished-functions 'TeX-revert-document-buffer))

  (use-package bibtex
    :after auctex
    :hook (bibtex-mode . penguin/bibtex-fill-column)
    :preface
    (defun penguin/bibtex-fill-column ()
      "Ensures that each entry does not exceed 120 characters."
      (setq fill-column 120)))

  (use-package company-auctex
    :after (auctex company)
    :config (company-auctex-init))

  (use-package company-math :after (auctex company))

  (setq-default TeX-engine 'xetex)

  (use-package reftex
    :after auctex
    :custom
    (reftex-plug-into-AUCTeX t)
    (reftex-save-parse-info t)
    (reftex-use-multiple-selection-buffers t))
#+end_src

** Lua
Lua is a perfect programming language for me. It is sad that it's not get the love it's deserved, but I will use it as much as possible for my personal project.
#+begin_src emacs-lisp :tangle yes
  (use-package lua-mode
    :delight "Œõ "
    :mode "\\.lua\\'"
    :interpreter ("lua" . lua-mode))
#+end_src

** Markdown
> Writing documents is a boring jobs ... when without org-mode or Markdown.
> - Quote by me.

#+begin_src emacs-lisp :tangle yes
  (use-package markdown-mode
    :ensure t
    :commands (markdown-mode gfm-mode)
    :mode (("README\\.md\\'" . gfm-mode)
           ("\\.md\\'" . markdown-mode)
           ("\\.markdown\\'" . markdown-mode))
    :init (setq markdown-command "multimarkdown"))
#+end_src

** Mermaid
I often use graph to demonstrate my thought of development process or to explain my workflow in presentation.
*Mermaid* is my choice for doing that job between all the others.

#+BEGIN_SRC emacs-lisp :tangle yes
  ;; (use-package mermaid-mode
  ;;   :ensure t
  ;;   :config
  ;;   (setq mermaid-mmdc-location "~/.emacs.d/.local/node_modules/.bin/mmdc"))
#+END_SRC

** Python
=lsp-mode= is not stable for Python (or at least for me).
So I try =anaconda-mode= for backend, and =company-anaconda= for frontend suggestion when working with Python.

#+begin_src emacs-lisp :tangle yes
  (defvar python-pyenv-root nil
    "The path to pyenv's root directory. This is automatically set when `python' is loaded.")
  (defvar python-pyenv-versions nil
    "Available versions of python in pyenv.")
  (defvar-local python-current-version nil
    "The current active pyenv version.")

  (use-package python
    :commands python-mode
    :init
    (setq python-enviroment-directory pcache-dir
          python-indent-gues-indent-offset-verbose nil
          python-shell-interpreter "python")
    :config
    (add-hook 'python-mode-hook #'flycheck-mode)
    (when (executable-find "ipython")
      (setq python-shell-interpreter "ipython"
            python-shell-interpreter-args "-i --simple-prompt --no-color-info"
            python-shell-prompt-regexp "In \\[[0-9]+\\]: "
            python-shell-prompt-block-regexp "\\.\\.\\.\\.: "
            python-shell-prompt-output-regexp "Out\\[[0-9]+\\]: "
            python-shell-completion-setup-code
            "from IPython.core.completerlib import module_completion"
            python-shell-completion-string-code
            "';'.join(get_ipython().Completer.all_completions('''%s'''))\n"))

    ;; Version Management
    (defun penguin/python-add-ver-to-modeline ()
      "Add version string to the major mode in the modeline."
      (setq mode-name
            (if python-current-version
                (format "Python %s" python-current-version)
              "Python")))
    (add-hook 'python-mode-hook #'penguin/python-add-ver-to-modeline)

    (if (not (executable-find "pyenv"))
        (setq python-current-version (string-trim (shell-command-to-string "python --version 2>&1 | cut -d' ' -f2")))
      (setq python-pyenv-root     (string-trim (shell-command-to-string "pyenv root"))
            python-pyenv-versions (split-string (shell-command-to-string "pyenv versions --bare") "\n" t))

      (defun penguin/python-detect-pyenv-version ()
        "Detect the pyenv version for the current project and set the relevant environment variables."
        (when-let* ((version-str (shell-command-to-string "python --version 2>&1 | cut -d' ' -f2")))
          (setq version-str (string-trim version-str)
                python-current-version version-str)
          (let ((pyenv-current-path (concat python-pyenv-root "/versions/" version-str)))
            (when (file-directory-p pyenv-current-path)
              (setq pythonic-environment pyenv-current-path)))
          (when (member version-str python-pyenv-versions)
            (setenv "PYENV_VERSION" version-str))))
      (add-hook 'python-mode-hook #'penguin/python-detect-pyenv-version)))

  (use-package anaconda-mode
    :after python
    :hook python-mode
    :init
    (setq anaconda-mode-installation-directory (concat petc-dir "anaconda/")
          anaconda-mode-eldoc-as-single-line t)
    :config
    (add-hook 'anaconda-mode-hook #'anaconda-eldoc-mode))

  (use-package company-anaconda
    :after anaconda-mode)

  (use-package pip-requirements
    :mode ("requirements.txt$" . pip-requirements-mode))

  (use-package nose
    :commands nose-mode
    :preface
    (defvar nose-mode-map (make-sparse-keymap)))
#+end_src

** XML
I hate XML. But my work still requires it sometimes...
#+begin_src emacs-lisp :tangle yes
  (use-package xml-mode
    :ensure nil
    :mode ("\\.wsdl\\'" "\\.xsd\\'"))
#+end_src

** YAML
This one is different from its infamous brother. It is still lovable.
#+begin_src emacs-lisp :tangle yes
  (use-package yaml-mode
    :delight "œà "
    :mode "\\.yml\\'"
    :interpreter ("yml" . yml-mode))
#+end_src


* User Interface
** Base
First, I'd want to turn off some mouse interface. I'd rather using keyboard all the time.
#+begin_src emacs-lisp :tangle yes
  (custom-set-variables '(menu-bar-mode nil)
                        '(scroll-bar-mode nil)
                        '(tool-bar-mode nil)
                        '(blink-cursor-mode nil))
#+end_src

*** Cursor
Some basic settings for the mouse cursor.
#+begin_src emacs-lisp :tangle yes
  (setq-default cursor-in-non-selected-windows 'hollow)
  (setq highlight-nonselected-windows nil)
  (setq x-stretch-cursor nil)
  (setq visible-cursor nil)
#+end_src

*** Icon
Most UI package require =all-the-icons= setup. Please remember to run =all-the-icons-install-fonts= for the first time runner.
#+begin_src emacs-lisp :tangle yes
  (use-package all-the-icons
    :config (setq all-the-icons-scale-factor 1.0))

  (use-package all-the-icons-ivy
    :hook (after-init . all-the-icons-ivy-setup))

  (use-package all-the-icons-dired)
  (add-hook 'dired-mode-hook 'all-the-icons-dired-mode)
#+end_src

*** Font
I really like Jetbrain font. To me, its a perfect font for an editor. Please setup the font in the =/res= folder to your OS first.
#+begin_src emacs-lisp :tangle yes
  (require 'font-lock)
  (set-face-attribute 'default nil
                      :font "JetBrains Mono"
                      :height (cond (sys/macos 110)
                                    (sys/win32 90)
                                    (sys/linux 90)
                                    (t 100)))
#+end_src

*** Frame Title
Some config for a better frame title, which I rarely see...
#+begin_src emacs-lisp :tangle yes
  (setq frame-title-format '("" "%b - Penguin Emacs üêß")
        icon-title-format frame-title-format)
#+end_src

** Dashboard
This one took me a lot of time. I try to customize my own dashboard, which looks like the doom dashboard somehow but I dont use =doom-emacs= üòù.
Since I got problems with displaying =all-the-icons= within string on Windows, so I need to add more conditions.
#+begin_src emacs-lisp :tangle yes
  (defcustom e_logo (expand-file-name "res/banner/penmacs_ascii_alter_2.png" user-emacs-directory)
    "Set up custom logo for the dashboard."
    :type 'string)

  (use-package dashboard
    :bind (("<f4>" . open-dashboard)
           :map dashboard-mode-map
           ("SPC r" . penguin/browse-recents)
           ("SPC p" . penguin/browse-projects)
           ("SPC c" . penguin/browse-calendar)
           ("SPC w" . penguin/browse-weather)
           ("SPC m" . penguin/browse-gmail)
           ("SPC s" . penguin/browse-slack)
           ("SPC h" . penguin/browse-homepage)
           ("<f4>" . quit-dashboard))
    :ensure t
    :config
    (setq dashboard-startup-banner (or e_logo 'official)
          dashboard-banner-logo-title (concat "v. " pemacs/version)
          dashboard-set-navigator nil
          dashboard-center-content t
          ;; dashboard-items '((recents  . 5)
          ;;                   (projects . 5)
          ;;                   (agenda   . 5)
          ;;                  )
          dashboard-items nil
          dashboard-set-footer t
          dashboard-footer-messages '(" P E N G U I N  x  E M A C S")
          dashboard-footer-icon (all-the-icons-faicon "heart"
                                                      :height 1.1
                                                      :v-adjust -0.05
                                                      :face 'font-lock-keyword-face)
          dashboard-set-heading-icons t
          dashboard-set-file-icons t
          show-week-agenda-p t)
    ;; Insert custom item
    (defface penguin/ultilities-face
      '((t . (:height 1.2 :foreground "#ffaf00" :weight bold)))
      "A face for ultilities.")
    (defface penguin/title-face
      '((t . (:height 1.5 :foreground "#ff4400")))
      "A face for title.")
    (defun dashboard-insert-custom (list-size)
      (when (or sys/linux sys/macos)
        ;; Working
        (insert (if (display-graphic-p)
                    (all-the-icons-faicon "code" :height 1.2 :v-adjust -0.05 :face 'error) " "))
        (let ((items  " Working: \n\n"))
          (put-text-property 0 (length items) 'face 'penguin/title-face
                             items)
          (insert items))
        (let ((items  (concat "      ÔÉÖ Recently opened files                   (SPC r)  \n\n"
                              "      Ó°ê Open Project                            (SPC p)  \n\n")))
          (put-text-property 0 (length items) 'face 'penguin/ultilities-face
                             items)
          (insert items))
        ;; Ultilities
        (insert (if (display-graphic-p)
                    (all-the-icons-faicon "list-ul" :height 1.2 :v-adjust -0.05 :face 'error) " "))
        (let ((items  " Ultilities: \n\n"))
          (put-text-property 0 (length items) 'face 'penguin/title-face
                             items)
          (insert items))
        (let ((items  (concat "      Ôâ¥ Open Org Agenda                         (SPC c)  \n\n"
                              "      ÔÄå View Weather forcast                    (SPC w)  \n\n"
                              "      ÓÖò Reading Mails                           (SPC m)  \n\n"
                              "      ÓÇ∑ Online Slack                            (SPC s)  \n\n"
                              "      ÔÑì Browse Github Homepage                  (SPC h)  ")))
          (put-text-property 0 (length items) 'face 'penguin/ultilities-face
                             items)
          (insert items)))

      (when sys/win32
        ;; Working
        (insert (if (display-graphic-p)
                    (all-the-icons-faicon "code" :height 1.2 :v-adjust -0.05 :face 'error) " "))
        (let ((items  " Working: \n\n"))
          (put-text-property 0 (length items) 'face 'penguin/title-face
                             items)
          (insert items))
        (let ((items  (concat "      üìù Recently opened files                   (SPC r)  \n\n"
                              "      üìö Open Project                            (SPC p)  \n\n")))
          (put-text-property 0 (length items) 'face 'penguin/ultilities-face
                             items)
          (insert items))

        ;; Ultilities
        (insert (if (display-graphic-p)
                    (all-the-icons-faicon "list-ul" :height 1.2 :v-adjust -0.05 :face 'error) " "))
        (let ((items  " Ultilities: \n\n"))
          (put-text-property 0 (length items) 'face 'penguin/title-face
                             items)
          (insert items))
        (let ((items  (concat "      üóì Open Org Agenda                         (SPC c)  \n\n"
                              "      ‚õÖ View Weather forcast                    (SPC w)  \n\n"
                              "      üìß Reading Mails                           (SPC m)  \n\n"
                              "      üí¨ Online Slack                            (SPC s)  \n\n"
                              "      üåé Browse Github Homepage                  (SPC h)  ")))
          (put-text-property 0 (length items) 'face 'penguin/ultilities-face
                             items)
          (insert items))))

    (add-to-list 'dashboard-item-generators  '(custom . dashboard-insert-custom))
    (add-to-list 'dashboard-items '(custom) t)
    (add-to-list 'dashboard-item-generators '(packages . dashboard-load-packages))

    (defun open-dashboard ()
      "Open the *dashboard* buffer and jump to the first widget."
      (interactive)
      (delete-other-windows)
      ;; Refresh dashboard buffer
      (if (get-buffer dashboard-buffer-name)
      (kill-buffer dashboard-buffer-name))
      (dashboard-insert-startupify-lists)
      (switch-to-buffer dashboard-buffer-name))
    (defun quit-dashboard ()
      "Quit dashboard window."
      (interactive)
      (quit-window t)
      (when (and dashboard-recover-layout-p
             (bound-and-true-p winner-mode))
        (winner-undo)
        (setq dashboard-recover-layout-p nil)))
    (defun penguin/browse-recents ()
      "Open recent files."
      (interactive)
      (counsel-recentf))
    (defun penguin/browse-projects ()
      "Open projectiles."
      (interactive)
      (helm-projectile-switch-project))
    (defun penguin/browse-calendar ()
      "Open the org-agenda."
      (interactive)
      (open-dashboard)
      (split-window-right)
      (other-window 1)
      (let ((org-agenda-window-setup 'current-window))
        (org-agenda nil "n")))
    (defun penguin/browse-weather ()
      "Open the weather forcast."
      (interactive)
      (wttrin "Hochiminh"))
    (defun penguin/browse-homepage ()
      "Open my github homepage."
      (interactive)
      (lambda (&rest _) (browse-url my-homepage))))

  (add-hook 'prog-mode-hook 'page-break-lines-mode) ;; Requirements for emacs dashboard packages
  (dashboard-setup-startup-hook)
#+end_src

** Highlight
Highlight current line.
#+begin_src emacs-lisp :tangle yes
  (use-package hl-line
    ;; Highlights the current line
    :hook ((prog-mode text-mode conf-mode) . hl-line-mode)
    :config
    (setq hl-line-sticky-flag nil
          global-hl-line-sticky-flag nil))
#+end_src

Highlight symbol: Currently I am turn off this mode globally for performance issue.
#+begin_src emacs-lisp :tangle yes
  (use-package highlight-symbol
    :ensure t
    :bind (("C-M-n" . highlight-symbol-next)
           ("C-M-p" . highlight-symbol-prev))
    :config
    (highlight-symbol-nav-mode))
#+end_src

Highlight matching parens
#+begin_src emacs-lisp :tangle yes
  (use-package paren
    :ensure nil
    :hook (after-init . show-paren-mode)
    :init (setq show-paren-when-point-inside-paren t
                show-paren-when-point-in-periphery t)
    :config
    (with-no-warnings
      (defun display-line-overlay (pos str &optional face)
        "Display line at POS as STR with FACE.
  FACE defaults to inheriting from default and highlight."
        (let ((ol (save-excursion
                    (goto-char pos)
                    (make-overlay (line-beginning-position)
                                  (line-end-position)))))
          (overlay-put ol 'display str)
          (overlay-put ol 'face
                       (or face '(:inherit highlight)))
          ol))

      (defvar-local show-paren--off-screen-overlay nil)
      (defun show-paren-off-screen (&rest _args)
        "Display matching line for off-screen paren."
        (when (overlayp show-paren--off-screen-overlay)
          (delete-overlay show-paren--off-screen-overlay))
        ;; check if it's appropriate to show match info,
        (when (and (overlay-buffer show-paren--overlay)
                   (not (or cursor-in-echo-area
                            executing-kbd-macro
                            noninteractive
                            (minibufferp)
                            this-command))
                   (and (not (bobp))
                        (memq (char-syntax (char-before)) '(?\) ?\$)))
                   (= 1 (logand 1 (- (point)
                                     (save-excursion
                                       (forward-char -1)
                                       (skip-syntax-backward "/\\")
                                       (point))))))
          ;; rebind `minibuffer-message' called by
          ;; `blink-matching-open' to handle the overlay display
          (cl-letf (((symbol-function #'minibuffer-message)
                     (lambda (msg &rest args)
                       (let ((msg (apply #'format-message msg args)))
                         (setq show-paren--off-screen-overlay
                               (display-line-overlay
                                (window-start) msg ))))))
            (blink-matching-open))))
      (advice-add #'show-paren-function :after #'show-paren-off-screen)))
#+end_src

** Modeline
After trying a lot of packages, I think =moody= fit me most as it is simple but beautiful in a technical way.
#+begin_src emacs-lisp :tangle yes
  ;; (use-package moody
  ;;   :config
  ;;   (let ((line (face-attribute 'mode-line :underline)))
  ;;     (set-face-attribute 'mode-line          nil :overline   line)
  ;;     (set-face-attribute 'mode-line-inactive nil :overline   line)
  ;;     (set-face-attribute 'mode-line-inactive nil :underline  line)
  ;;     (set-face-attribute 'mode-line          nil :box        nil)
  ;;     (set-face-attribute 'mode-line-inactive nil :box        nil)
  ;;     (set-face-attribute 'mode-line-inactive nil :background "#f9f2d9"))
  ;;   (setq x-underline-at-descent-line t))

  ;; (moody-replace-mode-line-buffer-identification)
  ;; (moody-replace-vc-mode)

  ;; A minor-mode menu for mode-line
  (use-package minions
    :hook (after-init . minions-mode)
    :init (setq minions-mode-line-lighter "‚ù§"))

  (use-package time
    :defer t
    :custom
    (display-time-default-load-average nil)
    (display-time-24hr-format t)
    :config
    (display-time-mode t)
    (display-battery-mode t))
#+end_src

Maybe I want to change to =doom-modeline= someday because it looks minimalism, but now **moody** still can statisfy me ü§£.
This package is stay here for investigation purpose:
#+BEGIN_SRC emacs-lisp :tangle yes
  (use-package doom-modeline
    :ensure t)

  (setq doom-modeline-height 25)
  (setq doom-modeline-bar-width 3)
  (setq doom-modeline-window-width-limit fill-column)
  (setq doom-modeline-project-detection 'project)
  (setq doom-modeline-buffer-file-name-style 'auto)
  (setq doom-modeline-icon (display-graphic-p))
  (setq doom-modeline-major-mode-icon t)
  (setq doom-modeline-major-mode-color-icon t)
  (setq doom-modeline-buffer-state-icon t)
  (setq doom-modeline-buffer-modification-icon t)
  (setq doom-modeline-lsp t)
  (setq doom-modeline-mu4e nil) ;; require mu4e-alert packages
  (setq doom-modeline-gnus t)
  (setq doom-modeline-gnus-timer 2)
  (doom-modeline-mode)
#+END_SRC

** M-x UI Enhancement
I dont like postframe and I like my minibuffer to stay organized.
#+begin_src emacs-lisp :tangle yes
  (use-package fancy-narrow
    :diminish
    :hook (after-init . fancy-narrow-mode))

  (setq resize-mini-windows 'grow-only
        ;; But don't let the minibuffer grow beyond this size
        max-mini-window-height 0.15)

  ;; M-x history
  (use-package smex
    :ensure t
    :config
    (setq smex-save-file (concat pcache-dir ".smex-items"))
    (smex-initialize))
#+end_src

** Shackle
Some packages would popup many frames randomly. I would them to stay under my control üòõ.
#+begin_src emacs-lisp :tangle yes
  (defvar shackle--popup-window-list nil) ; all popup windows
  (defvar-local shackle--current-popup-window nil) ; current popup window
  (put 'shackle--current-popup-window 'permanent-local t)

  (use-package shackle
    :functions org-switch-to-buffer-other-window
    :commands shackle-display-buffer
    :hook (after-init . shackle-mode)
    :config
    (eval-and-compile
      (defun shackle-last-popup-buffer ()
        "View last popup buffer."
        (interactive)
        (ignore-errors
          (display-buffer shackle-last-buffer)))
      (bind-key "C-h z" #'shackle-last-popup-buffer)

      ;; Add keyword: `autoclose'
      (defun shackle-display-buffer-hack (fn buffer alist plist)
        (let ((window (funcall fn buffer alist plist)))
          (setq shackle--current-popup-window window)

          (when (plist-get plist :autoclose)
            (push (cons window buffer) shackle--popup-window-list))
          window))

      (defun shackle-close-popup-window-hack (&rest _)
        "Close current popup window via `C-g'."
        (setq shackle--popup-window-list
              (cl-loop for (window . buffer) in shackle--popup-window-list
                       if (and (window-live-p window)
                               (equal (window-buffer window) buffer))
                       collect (cons window buffer)))
        ;; `C-g' can deactivate region
        (when (and (called-interactively-p 'interactive)
                   (not (region-active-p)))
          (let (window buffer)
            (if (one-window-p)
                (progn
                  (setq window (selected-window))
                  (when (equal (buffer-local-value 'shackle--current-popup-window
                                                   (window-buffer window))
                               window)
                    (winner-undo)))
              (setq window (caar shackle--popup-window-list))
              (setq buffer (cdar shackle--popup-window-list))
              (when (and (window-live-p window)
                         (equal (window-buffer window) buffer))
                (delete-window window)

                (pop shackle--popup-window-list))))))

      (advice-add #'keyboard-quit :before #'shackle-close-popup-window-hack)
      (advice-add #'shackle-display-buffer :around #'shackle-display-buffer-hack))

    ;; HACK: compatibility issuw with `org-switch-to-buffer-other-window'
    (advice-add #'org-switch-to-buffer-other-window :override #'switch-to-buffer-other-window)

    ;; rules
    (setq shackle-default-size 0.4
          shackle-default-alignment 'below
          shackle-default-rule nil
          shackle-rules
          '((("*Help*" "*Apropos*") :select t :size 0.3 :align 'below :autoclose t)
            (("*compilation*" "*Compile-Log*") :select t :size 0.3 :align 'below :autoclose t)
            ("*Completions*" :size 0.3 :align 'below :autoclose t)
            ("*Pp Eval Output*" :size 15 :align 'below :autoclose t)
            ("*Backtrace*" :select t :size 15 :align 'below)
            (("*Warnings*" "*Messages*") :size 0.3 :align 'below :autoclose t)
            ("^\\*.*Shell Command.*\\*$" :regexp t :size 0.3 :align 'below :autoclose t)
            ("\\*[Wo]*Man.*\\*" :regexp t :select t :align 'below :autoclose t)
            ("*Calendar*" :select t :size 0.3 :align 'below)
            (("*shell*" "*eshell*" "*ielm*") :popup t :align 'below)
            ("^\\*vc-.*\\*$" :regexp t :size 0.3 :align 'below :autoclose t)
            ("*gud-debug*" :select t :size 0.4 :align 'below :autoclose t)
            ("\\*ivy-occur .*\\*" :regexp t :select t :align 'below)
            (" *undo-tree*" :select t)
            ("*quickrun*" :select t :size 15 :align 'below)
            ("*Finder*" :select t :size 0.3 :align 'below :autoclose t)
            ("^\\*macro expansion\\**" :regexp t :size 0.4 :align 'below)
            ("^\\*elfeed-entry" :regexp t :size 0.7 :align 'below :autoclose t)
            ((" *Org todo*" "*Org Dashboard*" "*Org Select*") :select t :size 0.4 :align 'below :autoclose t)
            (("*lsp-help*" "*lsp session*") :size 0.3 :align 'below :autoclose t)
            (" *Install vterm" :size 0.35 :same t :align 'below)
            (("*Paradox Report*" "*package update results*") :size 0.2 :align 'below :autoclose t)
            ("*Package-Lint*" :size 0.4 :align 'below :autoclose t)
            (("*Gofmt Errors*" "*Go Test*") :select t :size 0.3 :align 'below :autoclose t)
            ("*How Do You*" :select t :size 0.5 :align 'below :autoclose t)

            ("*ert*" :size 15 :align 'below :autoclose t)
            (overseer-buffer-mode :size 15 :align 'below :autoclose t)

            (" *Flycheck checkers*" :select t :size 0.4 :align 'below :autoclose t)
            ((flycheck-error-list-mode flymake-diagnostics-buffer-mode)
             :select t :size 0.3 :align 'below :autoclose t)

            ("*Ibuffer*" :select t :size 0.4 :align 'below :autoclose t)
            (ibuffer-mode :select t :size 0.4 :align 'below :autoclose t)

            ("*Org Agenda*" :select t :size 0.4 :align 'right :autoclose t)
            (org-super-agenda-mode :select t :size 0.4 :align 'right :autoclose t)

            (profiler-report-mode :select t :size 0.5 :align 'below)
            ("*ELP Profiling Restuls*" :select t :size 0.5 :align 'below)

            ((inferior-python-mode inf-ruby-mode swift-repl-mode) :size 0.4 :align 'below)
            ("*prolog*" :size 0.4 :align 'below)

            ("*anaconda-mode*" :noselect t :size 10 :autoclose t :autokill t)
            (anaconda-mode :noselect t :size 10 :autoclose t :autokill t)

            ("*nose-mode*" :noselect t :size 0.4)
            (nose-mode :noselect t :size 0.4)

            ((grep-mode rg-mode deadgrep-mode ag-mode pt-mode) :select t :align 'below)
            (Buffer-menu-mode :select t :size 20 :align 'below :autoclose t)
            (helpful-mode :select t :size 0.3 :align 'below :autoclose t)
            ((process-menu-mode cargo-process-mode) :select t :size 0.3 :align 'below :autoclose t)
            (list-environment-mode :select t :size 0.3 :align 'below :autoclose t)
            (tabulated-list-mode :size 0.4 :align 'below))))
#+end_src

** Treemacs
A side explorer for Emacs, it always nice to be more modern looking.
#+begin_src emacs-lisp :tangle yes
  (use-package treemacs
    :ensure t
    :defer t
    :init
    (with-eval-after-load 'winum
      (define-key winum-keymap (kbd "M-0") #'treemacs-select-window))
    :config
    (progn
      (setq treemacs-collapse-dirs                 (if treemacs-python-executable 3 0)
            treemacs-deferred-git-apply-delay      0.5
            treemacs-directory-name-transformer    #'identity
            treemacs-display-in-side-window        t
            treemacs-eldoc-display                 t
            treemacs-file-event-delay              5000
            treemacs-file-extension-regex          treemacs-last-period-regex-value
            treemacs-file-follow-delay             0.2
            treemacs-file-name-transformer         #'identity
            treemacs-follow-after-init             t
            treemacs-git-command-pipe              ""
            treemacs-goto-tag-strategy             'refetch-index
            treemacs-indentation                   2
            treemacs-indentation-string            " "
            treemacs-is-never-other-window         nil
            treemacs-max-git-entries               500
            treemacs-no-delete-other-windows       t
            treemacs-missing-project-action        'ask
            treemacs-project-follow-cleanup        nil
            treemacs-persist-file                  (expand-file-name "treemacs-persist" pcache-dir)
            treemacs-position                      'left
            treemacs-recenter-distance             0.1
            treemacs-recenter-after-file-follow    nil
            treemacs-recenter-after-tag-follow     nil
            treemacs-recenter-after-project-jump   'always
            treemacs-recenter-after-project-expand 'on-distance
            treemacs-show-cursor                   nil
            treemacs-show-hidden-files             t
            treemacs-silent-filewatch              nil
            treemacs-silent-refresh                nil
            treemacs-sorting                       'alphabetic-asc
            treemacs-space-between-root-nodes      t
            treemacs-tag-follow-cleanup            t
            treemacs-tag-follow-delay              1.5
            treemacs-no-png-images                 nil
            treemacs-width                         30)

      (treemacs-follow-mode t)
      (treemacs-filewatch-mode t)
      (treemacs-fringe-indicator-mode t)
      (pcase (cons (not (null (executable-find "git")))
                   (not (null treemacs-python-executable)))
        (`(t . t)
         (treemacs-git-mode 'deferred))
        (`(t . _)
         (treemacs-git-mode 'simple))))
    :bind
    (:map global-map
          ("M-0"       . treemacs-select-window)
          ("C-x t 1"   . treemacs-delete-other-windows)
          ("C-x t t"   . treemacs)
          ("C-x t B"   . treemacs-bookmark)
          ("C-x t C-t" . treemacs-find-file)
          ("C-x t M-t" . treemacs-find-tag)))

  (use-package treemacs-projectile
    :after treemacs projectile)

  (use-package treemacs-magit
        :after magit
        :commands treemacs-magit--schedule-update
        :hook ((magit-post-commit
                git-commit-post-finish
                magit-post-stage
                magit-post-unstage)
               . treemacs-magit--schedule-update))
  ;; End `Treemacs'
  (setq inhibit-compacting-font-caches t) ;; For fixing the lag with all-the-icons
#+end_src

** Theme
=doom-themes= are quite good-looking and have a variety of options to choose.
#+begin_src emacs-lisp :tangle yes
  ;; (use-package doom-themes
  ;;   :defines doom-themes-treemacs-theme
  ;;   :config
  ;;   (doom-themes-visual-bell-config)
  ;;   (set-face-attribute 'doom-visual-bell nil
  ;;                       :inherit 'mode-line
  ;;                       :background (face-foreground 'error)
  ;;                       :inverse-video 'unspecified)
  ;;   (doom-themes-org-config)
  ;;   (with-eval-after-load 'erc-goodies
  ;;     (set-face-attribute 'erc-bold-face nil :bold nil))
  ;;   (with-eval-after-load 'flycheck
  ;;     (set-face-attribute 'flycheck-error nil :underline `(:color ,(doom-color 'error) :style line))
  ;;     (set-face-attribute 'flycheck-info nil :underline `(:color ,(doom-color 'highlight) :style line))
  ;;     (set-face-attribute 'flycheck-warning nil :underline `(:color ,(doom-color 'warning) :style line)))
  ;;   (with-eval-after-load 'isearch
  ;;     (set-face-attribute 'isearch nil :background (doom-color 'blue) :foreground (doom-color 'dark-blue))
  ;;     (set-face-attribute 'lazy-highlight nil :foreground (doom-color 'blue))))

  ;; (doom-themes-treemacs-config)
  ;; (setq doom-themes-treemacs-theme "doom-colors")
  ;; (load-theme 'doom-molokai t)
#+end_src

One theme cannot satisfy me, I will let this here for investigation. =spacemacs-themes= seems really good with **org-mode**
#+BEGIN_SRC emacs-lisp :tangle yes
  (use-package spacemacs-theme
    :defer t
    :init (load-theme 'spacemacs-dark t))
#+END_SRC

=kaolin-themes= is pretty nice in its own way and it provides a predefined icons packs.
#+begin_src emacs-lisp :tangle yes
  (use-package kaolin-themes)
  (kaolin-treemacs-theme)
#+end_src

For a more contrast looking, I use =solaire-mode=. Currently disable for conflicts with hline and other packages..
#+BEGIN_SRC emacs-lisp :tangle yes
  ;; (use-package solaire-mode
  ;;   :hook
  ;;   ((change-major-mode after-revert ediff-prepare-buffer) . turn-on-solaire-mode)
  ;;   (minibuffer-setup . solaire-mode-in-minibuffer)
  ;;   :config
  ;;   (solaire-global-mode +1))
#+END_SRC

** Winner
Tracking windows layout in emacs.
#+begin_src emacs-lisp :tangle yes
  (use-package winner
    :hook (after-init . winner-mode)
    :bind (("<s-right>" . winner-redo)
           ("<s-left>" . winner-undo)))
#+end_src


* Org Mode
** Sync Source File
I‚Äôm using a large =.org= file to maintain my GNU Emacs configuration. However, at launch, it will load the =config.el= source file for a faster loading.
The code below, executes =org-babel-tangle= asynchronously when =config.org= is saved.
#+begin_src emacs-lisp :tangle yes
  (use-package async)

  (defvar *config-file* (expand-file-name "config.org" user-emacs-directory)
    "The configuration file.")

  (defvar *config-last-change* (nth 5 (file-attributes *config-file*))
    "Last modification time of the configuration file.")

  (defvar *show-async-tangle-results* nil
    "Keeps *emacs* async buffers around for later inspection.")

  (defun penguin/config-updated ()
    "Checks if the configuration file has been updated since the last time."
    (time-less-p *config-last-change*
                 (nth 5 (file-attributes *config-file*))))

  (defun penguin/config-tangle ()
    "Tangles the org file asynchronously."
    (when (penguin/config-updated)
      (setq *config-last-change*
            (nth 5 (file-attributes *config-file*)))
      (penguin/async-babel-tangle *config-file*)))

  (defun penguin/async-babel-tangle (org-file)
    "Tangles the org file asynchronously."
    (let ((init-tangle-start-time (current-time))
          (file (buffer-file-name))
          (async-quiet-switch "-q"))
      (async-start
       `(lambda ()
          (require 'org)
          (org-babel-tangle-file ,org-file))
       (unless *show-async-tangle-results*
         `(lambda (result)
            (if result
                (message "SUCCESS: %s successfully tangled (%.2fs)."
                         ,org-file
                         (float-time (time-subtract (current-time)
                                                    ',init-tangle-start-time)))
              (message "ERROR: %s as tangle failed." ,org-file)))))))

#+end_src

** Org
When I knew about =org-mode=, I knew that Emacs will be my love forever.
First, I need to setup some basic function for boosting =org-mode= potiental.

#+begin_src emacs-lisp :tangle yes
  (defun penguin/find-project-task ()
    "Move point to the parent (project) task if any."
    (save-restriction
      (widen)
      (let ((parent-task))
        (while (and (not parent-task)
                    (org-up-heading-safe))
          (when (penguin/is-project-p)
            (setq parent-task (point))))
        parent-task)))

  (defun penguin/is-task-stuck ()
    "Return non-nil if the task at point is stuck."
    ;; (bh/list-sublevels-for-projects-indented)
    (save-restriction
      (widen)
      (let ((next-headline (save-excursion (or (outline-next-heading) (point-max))))
            (this-headline (save-excursion (org-back-to-heading 'invisible-ok) (point))))
         (let* ((subtree-end (save-excursion (org-end-of-subtree t)))
                (has-next))
            (save-excursion
              (forward-line 1)
              (while (and (not has-next)
                          (< (point) subtree-end)
                          (re-search-forward "^\\*+ NEXT\\|DELEGATED " subtree-end t))
                ;; Only skip if there is a deadline for delegated tasks
                (unless (and (member (org-get-todo-state) (list "DELEGATED"))
                             (not (org-element-property :deadline (org-element-at-point))))
                  (setq has-next t))))
            (not has-next)))))

  (defun penguin/is-project-p ()
    "Any task with a todo keyword subtask."
    (save-restriction
      (widen)
      (let ((has-subtask)
            (subtree-end (save-excursion (org-end-of-subtree t)))
            (is-a-task (and (nth 2 (org-heading-components))
                            (not (member (nth 2 (org-heading-components)) org-done-keywords)))))
        (save-excursion
          (forward-line 1)
          (while (and (not has-subtask)
                      (< (point) subtree-end)
                      (re-search-forward "^\*+ " subtree-end t))
            (let ((keyword (org-get-todo-state)))
              (when (and keyword
                         (not (member keyword (append '("LATER") org-done-keywords))))
                (setq has-subtask t)))))
        (and is-a-task has-subtask))))

  (defun penguin/should-skip ()
    "Return non-nil if all the project subtasks are not stuck."
    (interactive)
    (let ((next-heading (save-excursion (or (outline-next-heading) (point-max))))
          (this-headline (org-element-at-point)))
      (save-restriction
        (save-excursion
          (org-narrow-to-subtree)
          (let* ((headlines (org-element-map (org-element-parse-buffer 'greater-element t)
                                'headline
                              #'identity))
                 ;; Filter out element at point
                 (direct-children (seq-filter (lambda (headline)
                                                (not (equal (org-element-property :begin this-headline)
                                                            (org-element-property :begin headline))))
                                              headlines)))
            (penguin/should-skip-children direct-children))))))

  (defun penguin/should-skip-children (children)
    "Check if we should skip the CHILDREN."
    (widen)
    (if children
        ;; If there are any children, recursively see if any task in the subtree is stuck
        (if (seq-some (lambda (child)
                        (goto-char (org-element-property :begin child))
                        (not (penguin/should-skip)))
                      children)
            ;; If there are any task which should not be skipped, include it and continue.
            nil
          ;; Else we don't care about it
          next-heading)
      ;; No children, so check if it is a project task
      (if (penguin/find-project-task)
          (if (penguin/is-task-stuck)
              nil
            next-heading)
        next-heading)))

  (defun penguin-agenda-prefix ()
    "Define the prefix."
    (let* ((deadline (org-element-property :deadline (org-element-at-point)))
           (level (org-element-property :level (org-element-at-point)))
           (project-level (org-element-property :level (save-excursion
                                                         (bh/find-project-task t)
                                                         (org-element-at-point))))
           (adjusted (- level project-level))
           (category (org-entry-get (point) "CATEGORY")))
      (cond ((and deadline
                  (not (bh/is-subproject-p)))
             (org-timestamp-format deadline "%x"))
            ((and (bh/is-subproject-p)
                  (penguin/is-project-p))
             (penguin-agenda-make-prefix adjusted t))
            ((penguin/is-project-p)
             (concat category
                     ": "))
            (t (penguin-agenda-make-prefix adjusted)))))

  (defun penguin-agenda-make-prefix (level &optional subproject-p)
    "Make the agenda prefix with LEVEL and the SUBPROJECT-P."
    (let ((adjusted (+ 11 level)))
      (concat (make-string adjusted ?\s)
              (char-to-string (org-bullets-level-char level))
              " ")))

  (defun bh/find-project-task (&optional top)
    "Move point to the parent (project) TOP task if any."
    (save-restriction
      (widen)
      (let ((this-task (save-excursion (org-back-to-heading 'invisible-ok) (point)))
            (parent-task))
        (while (and (or top (not parent-task)) (org-up-heading-safe))
          (when (bh/is-project-p)
            (setq parent-task (point))))
        (if parent-task
            (progn
              (goto-char parent-task)
              parent-task)
          (goto-char this-task)
          this-task))))

  (defun bh/is-subproject-p ()
    "Any task which is a subtask of another project."
    (let ((is-subproject)
          (is-a-task (member (nth 2 (org-heading-components)) org-todo-keywords-1)))
      (save-excursion
        (while (and (not is-subproject) (org-up-heading-safe))
          (when (member (nth 2 (org-heading-components)) org-todo-keywords-1)
            (setq is-subproject t))))
      (and is-a-task is-subproject)))

  (defun bh/is-project-p ()
    "Any task with a todo keyword subtask."
    (save-restriction
      (widen)
      (let ((has-subtask)
            (subtree-end (save-excursion (org-end-of-subtree t)))
            (is-a-task (not (member (nth 2 (org-heading-components)) org-done-keywords))))
        (save-excursion
          (forward-line 1)
          (while (and (not has-subtask)
                      (< (point) subtree-end)
                      (re-search-forward "^\*+ " subtree-end t))
            (let ((keyword (org-get-todo-state)))
              (when (and keyword
                         (not (member keyword (append '("LATER") org-done-keywords))))
                (setq has-subtask t)))))
        (and is-a-task has-subtask))))
#+end_src

Now, the mighty **org-mode**
#+begin_src emacs-lisp :tangle yes
  (use-package org
    :ensure t
    :delight "Œò "
    :bind ("C-c i" . org-insert-structure-template)
    :preface
    (defun penguin/org-compare-times (clocked estimated)
      "Gets the ratio between the timed time and the estimated time."
      (if (and (> (length clocked) 0) estimated)
          (format "%.2f"
                  (/ (* 1.0 (org-hh:mm-string-to-minutes clocked))
                     (org-hh:mm-string-to-minutes estimated)))
        ""))
    (defun penguin/org-archive-done-tasks ()
      "Archives finished or cancelled tasks."
      (interactive)
      (org-map-entries
       (lambda ()
         (org-archive-subtree)
         (setq org-map-continue-from (outline-previous-heading)))
       "TODO=\"DONE\"|TODO=\"CANCELLED\"" (if (org-before-first-heading-p) 'file 'tree)))
    (defun penguin/org-jump ()
      "Jumps to a specific task."
      (interactive)
      (let ((current-prefix-arg '(4)))
        (call-interactively 'org-refile)))
    (defun penguin/org-use-speed-commands-for-headings-and-lists ()
      "Activates speed commands on list items too."
      (or (and (looking-at org-outline-regexp) (looking-back "^\**"))
          (save-excursion (and (looking-at (org-item-re)) (looking-back "^[ \t]*")))))
    (defmacro ignore-args (fnc)
      "Returns function that ignores its arguments and invokes FNC."
      `(lambda (&rest _rest)
         (funcall ,fnc)))
    :hook ((after-save . penguin/config-tangle)
           (auto-save . org-save-all-org-buffers)
           (org-mode . org-indent-mode))
    :custom
    ;; (org-archive-location "~/.emacs.d/.private/org/.archives/%s::")
    (org-blank-before-new-entry '((heading . t)
                                  (plain-list-item . t)))
    (org-cycle-include-plain-lists 'integrate)
    (org-expiry-inactive-timestamps t)
    (org-export-backends '(ascii beamer html icalendar latex man md org texinfo))
    (org-log-done 'time)
    (org-log-into-drawer "LOGBOOK")
    (org-modules '(org-crypt
                   org-habit
                   org-info
                   org-irc
                   org-mouse
                   org-protocol
                   org-tempo))
    (org-refile-allow-creating-parent-nodes 'confirm)
    (org-refile-use-cache nil)
    (org-refile-use-outline-path nil)
    (org-refile-targets '((org-agenda-files . (:maxlevel . 6))))
    (org-startup-folded nil)
    (org-startup-with-inline-images t)
    (org-tag-alist '(("@coding"   . ?c)
                     ("@computer" . ?l)
                     ("@home"     . ?h)
                     ("@phone"    . ?p)
                     ("@reading"  . ?r)
                     ("@work"     . ?b)
                     ("@writing"  . ?w)
                     ("crypt"     . ?C)
                     ("fuzzy"     . ?0)))
    (org-tags-exclude-from-inheritance '("crypt" "project"))
    (org-todo-keywords '((sequence "TODO(t)" "NEXT(n)" "|" "DONE(d)")
                         (sequence "NOTE(N)" "MEETING(M)")
                         (sequence "WAITING(w@)" "DELEGATED(e@)" "LATER(l)" "|" "CANCELLED(c)")
                         (sequence "TOBUY" "TOSHRINK" "TOCUT" "TOSEW" "|" "DONE(x)")))

    (org-use-effective-time t)
    (org-use-speed-commands 'penguin/org-use-speed-commands-for-headings-and-lists)
    (org-yank-adjusted-subtrees t)
    :config
    (add-to-list 'org-global-properties '("Effort_ALL". "0:05 0:15 0:30 1:00 2:00 3:00 4:00"))
    (add-to-list 'org-speed-commands-user '("!" penguin/org-clock-in-and-track))
    (add-to-list 'org-speed-commands-user '("$" call-interactively 'org-archive-subtree))
    (add-to-list 'org-speed-commands-user '("d" penguin/org-move-line-to-destination))
    (add-to-list 'org-speed-commands-user '("i" call-interactively 'org-clock-in))
    (add-to-list 'org-speed-commands-user '("o" call-interactively 'org-clock-out))
    (add-to-list 'org-speed-commands-user '("s" call-interactively 'org-schedule))
    (add-to-list 'org-speed-commands-user '("x" org-todo "DONE"))
    (add-to-list 'org-speed-commands-user '("y" org-todo-yesterday "DONE"))
    (advice-add 'org-deadline :after (ignore-args #'org-save-all-org-buffers))
    (advice-add 'org-schedule :after (ignore-args #'org-save-all-org-buffers))
    (advice-add 'org-store-log-note :after (ignore-args #'org-save-all-org-buffers))
    (advice-add 'org-todo :after (ignore-args #'org-save-all-org-buffers))
    (org-clock-persistence-insinuate)
    (org-load-modules-maybe t))

  (use-package org-indent :ensure nil :after org :delight)

  (use-package org-faces
    :ensure nil
    :after org
    :custom
    (org-todo-keyword-faces '(("WAITING" :foreground "#fabd2f" :weight bold)
                              ("DELEGATED" :foreground "#fabd2f" :weight bold)
                              ("NOTE" :foreground "#83a598" :weight bold)
                              ("MEETING" :foreground "#83a598" :weight bold)
                              ("LATER" :foreground "#83a598" :weight bold)
                              ("NEXT" :foreground "#b8bb26" :weight bold))))
#+end_src

** Org TOC
We can use =toc-org= package to generate a table of content.
#+begin_src emacs-lisp :tangle yes
  (use-package toc-org
    :after org
    :hook (org-mode . toc-org-enable))
#+end_src

** Org Agenda
I want to take down my notes, tasks to organize my work and I have tried so many softwares, and then, this packages come:
#+begin_src emacs-lisp :tangle yes
  (use-package org-agenda
    :ensure nil
    :after org
    :bind (:map org-agenda-mode-map
                ("X" . penguin/org-agenda-mark-done-and-add-followup)
                ("x" . penguin/org-agenda-done))
    :preface
    (defun penguin/org-agenda-done (&optional arg)
      "Mark current TODO as done.
    This changes the line at point, all other lines in the agenda referring to
    the same tree node, and the headline of the tree node in the Org-mode file."
      (interactive "P")
      (org-agenda-todo "DONE"))
    (defun penguin/org-agenda-mark-done-and-add-followup ()
      "Mark the current TODO as done and add another task after it.
     Creates it at the same level as the previous task, so it's better to use
     this with to-do items than with projects or headings."
      (interactive)
      (org-agenda-todo "DONE")
      (org-agenda-switch-to)
      (org-capture 0 "t"))
    :custom
    (org-agenda-dim-blocked-tasks t)
    (org-directory "~/.emacs.d/.private/org/")
    ;; (org-default-notes-file "~/.personal/agenda/organizer.org")
    (org-agenda-files (list org-directory))
    (org-agenda-inhibit-startup t)
    (org-agenda-show-log t)
    (org-agenda-skip-deadline-prewarning-if-scheduled 'pre-scheduled)
    (org-agenda-span 5)
    (org-agenda-start-on-weekday 6)
    (org-agenda-sticky nil)
    (org-agenda-tags-column -100)
    (org-agenda-use-tag-inheritance t)
    (org-columns-default-format "%14SCHEDULED %Effort{:} %1PRIORITY %TODO %50ITEM %TAGS")
    (org-enforce-todo-dependencies t)
    (org-habit-completed-glyph ?‚úì)
    (org-habit-graph-column 80)
    (org-habit-show-habits-only-for-today nil)
    (org-habit-today-glyph ?‚Äñ)
    (org-track-ordered-property-with-tag t)
    (org-agenda-time-grid '((daily today require-timed)))
    (org-agenda-skip-scheduled-if-done t)
    (org-agenda-skip-deadline-if-done t)
    (org-agenda-include-deadlines t)
    (org-agenda-include-diary t))
#+end_src

And its super brother
#+begin_src emacs-lisp :tangle yes
  (use-package org-super-agenda)
  (setq penguin-org-deadline-prefix "%2i%-12(penguin-agenda-prefix)")
  (setq org-agenda-custom-commands '(("n" "Agenda"
                                      ((agenda "")
                                       (todo ""
                                             ((org-agenda-overriding-header (concat
                                                                             (if (or sys/macos sys/linux)
                                                                                 (all-the-icons-faicon "chain-broken" :v-adjust 0.01)
                                                                               "üîó")
                                                                             " Stuck Projects"))
                                              (org-agenda-skip-function #'penguin/should-skip)
                                              (org-agenda-prefix-format penguin-org-deadline-prefix)
                                              (org-agenda-sorting-strategy nil)))
                                       (todo "NEXT"
                                             ((org-agenda-overriding-header (concat
                                                                             (if (or sys/macos sys/linux)
                                                                                 (all-the-icons-faicon "bolt" :v-adjust 0.01)
                                                                               "‚ö°"
                                                                             )
                                                                             " Next Tasks"))
                                              (org-agenda-sorting-strategy '(priority-down category-up))))
                                       (todo "TODO"
                                             ((org-agenda-files '("~/.emacs.d/.private/org/todo.org" "~/.emacs.d/.private/org/notes.org"))
                                              (org-agenda-sorting-strategy '(priority-down category-up))
                                              (org-agenda-overriding-header (concat
                                                                             (if (or sys/linux sys/macos)
                                                                                 (all-the-icons-faicon "check-square-o" :v-adjust 0.01)
                                                                               "‚òë")
                                                                             " Tasks"))))
                                       (todo "WAITING|DELEGATED"
                                             ((org-agenda-overriding-header (concat
                                                                             (if (or sys/linux sys/macos)
                                                                                 (all-the-icons-faicon "hourglass" :v-adjust 0.01)
                                                                               "‚åõ")
                                                                             " Waiting/Delegated"))
                                              (org-agenda-sorting-strategy '(priority-down category-up))))
                                       (todo "LATER"
                                             ((org-agenda-sorting-strategy '(priority-down category-up))
                                              (org-agenda-overriding-header (concat
                                                                             (if (or sys/linux sys/macos)
                                                                                 (all-the-icons-faicon "thumb-tack" :v-adjust 0.01)
                                                                               "üí£")
                                                                             " Later"))))
                                       (todo "NOTE"
                                             ((org-agenda-overriding-header (concat
                                                                             (if (or sys/macos sys/linux)
                                                                                 (all-the-icons-faicon "sticky-note" :v-adjust 0.01)
                                                                               "üóí")
                                                                             " Notes"))
                                              (org-agenda-max-entries 10)
                                              (org-agenda-sorting-strategy '(tsia-down)))))
                                      nil)))
  (org-super-agenda-mode)
  (setq split-height-threshold 40) ; nil
  (setq split-width-threshold nil) ; 100
#+end_src

** Org Bullet
For a better visual
#+begin_src emacs-lisp :tangle yes
  (use-package org-bullets
    :hook (org-mode . org-bullets-mode)
    :custom
    (org-bullets-bullet-list '("‚óâ" "‚óã"))
    (org-hide-emphasis-markers nil)
    (org-list-demote-modify-bullet '(("+" . "-") ("1." . "a.") ("-" . "+")))
    (org-ellipsis "‚ñº")
    (org-emphasis-alist '(("*" (bold :foreground "Orange" ))
                          ("/" italic)
                          ("_" underline)
                          ("=" (:foreground "maroon"))
                          ("~" (:foreground "deep sky blue"))
                          ("+" (:strike-through t)))))
#+end_src

** Org Capture
A package to speed up =org-mode=
#+begin_src emacs-lisp :tangle yes
  (use-package org-capture
    :ensure nil
    :after org
    :custom
    (+org-capture-todo-file "~/.emacs.d/.private/org/todo.org")
    (+org-capture-notes-file "~/.emacs.d/.private/org/notes.org")
    (+org-capture-routine-file "~/.emacs.d/.private/org/routine.org")
    (org-capture-templates '(("t" "Todo" entry
                              (file+headline +org-capture-todo-file "Inbox")
                              "* TODO %?" :prepend t :kill-buffer t)
                             ("n" "Next" entry
                              (file+headline +org-capture-todo-file "Inbox")
                              "* NEXT %?" :prepend t :kill-buffer t)
                             ("w" "Waiting" entry
                              (file+headline +org-capture-todo-file "Inbox")
                              "* WAITING %?" :prepend t :kill-buffer t)
                             ("r" "Routine" entry
                              (file+headline +org-capture-routine-file "Daily")
                              "* %^{Type?|TODO|NEXT|WAITING|DELEGATED} %^{Content?} :routine:\nSCHEDULED: %t\n%a" :pretend t :kill-buffer t)
                             ("o" "Email Note" entry
                              (file+headline +org-capture-notes-file "Inbox")
                              "* NOTE %u %^{Content?} :email:\n%a" :prepend t :kill-buffer t)
                             ("e" "Email" entry
                              (file+headline +org-capture-todo-file "Inbox")
                              "* %^{Type?|TODO|NEXT|WAITING|DELEGATED} %^{Content?} :email:\nSCHEDULED: %t\n%a" :prepend t :kill-buffer t)
                             ("m" "Meeting Notes" entry
                              (file+headline +org-capture-notes-file "Inbox")
                              "* NOTE %u %? :meeting:\n** Present at meeting\n- [ ] \n** Agenda\n** Notes" :prepend t :kill-buffer t)
                             ("N" "Notes" entry
                              (file+headline +org-capture-notes-file "Inbox")
                              "* NOTE %u %?\n%i" :prepend t :kill-buffer t))))
#+end_src

** Org Clock
#+begin_src emacs-lisp :tangle yes
  (use-package org-clock
    :ensure nil
    :after org
    :preface
    (defun penguin/org-mode-ask-effort ()
      "Ask for an effort estimate when clocking in."
      (unless (org-entry-get (point) "Effort")
        (let ((effort
               (completing-read
                "Effort: "
                (org-entry-get-multivalued-property (point) "Effort"))))
          (unless (equal effort "")
            (org-set-property "Effort" effort)))))
    :hook (org-clock-in-prepare-hook . penguin/org-mode-ask-effort)
    :custom
    (org-clock-clocktable-default-properties
     '(:block day :maxlevel 2 :scope agenda :link t :compact t :formula %
              :step day :fileskip0 t :stepskip0 t :narrow 80
              :properties ("Effort" "CLOCKSUM" "CLOCKSUM_T" "TODO")))
    (org-clock-continuously nil)
    (org-clock-in-switch-to-state "STARTED")
    (org-clock-out-remove-zero-time-clocks t)
    (org-clock-persist t)
    (org-clock-persist-file (expand-file-name "~/.emacs.d/.local/org-clock-save.el" user-emacs-directory))
    (org-clock-persist-query-resume nil)
    (org-clock-report-include-clocking-task t)
    (org-show-notification-handler (lambda (msg) (alert msg))))
#+end_src

** Org Journal
Do I need to write diary????
#+begin_src emacs-lisp :tangle yes
(use-package org-journal
  :after org
  :bind (("C-c T" . org-journal-new-entry)
         ("C-c Y" . journal-file-yesterday))
  :preface
  (defun get-journal-file-yesterday ()
    "Gets filename for yesterday's journal entry."
    (let* ((yesterday (time-subtract (current-time) (days-to-time 1)))
           (daily-name (format-time-string "%Y%m%d" yesterday)))
      (expand-file-name (concat org-journal-dir daily-name))))
  (defun journal-file-yesterday ()
    "Creates and load a file based on yesterday's date."
    (interactive)
    (find-file (get-journal-file-yesterday)))
  :custom
  (org-journal-date-format "%e %b %Y (%A)")
  (org-journal-dir "~/.emacs.d/.private/org/journal/")
  (org-journal-file-type 'weekly)
  (org-journal-enable-encryption t)
  (org-journal-file-format "%Y%m%d")
  (org-journal-time-format ""))
#+end_src

** Obx
Compiler for other languages
#+begin_src emacs-lisp :tangle yes
  (use-package ob-C :ensure nil :after org)
  (use-package ob-css :ensure nil :after org)
  (use-package ob-ditaa :ensure nil :after org)
  (use-package ob-dot :ensure nil :after org)
  (use-package ob-emacs-lisp :ensure nil :after org)
  (use-package ob-gnuplot :ensure nil :after org)
  (use-package ob-java :ensure nil :after org)
  (use-package ob-js :ensure nil :after org)

  (use-package ob-latex
    :ensure nil
    :after org
    :custom (org-latex-compiler "xelatex"))

  (use-package ob-ledger :ensure nil :after org)
  (use-package ob-makefile :ensure nil :after org)
  (use-package ob-org :ensure nil :after org)

  ;; (use-package ob-plantuml
  ;;   :ensure nil
  ;;   :after org
  ;;   :custom (org-plantuml-jar-path (expand-file-name (format "%s/plantuml.jar" xdg-lib))))

  (use-package ob-mermaid
    :ensure t
    :after org
    :config
    (setq ob-mermaid-cli-path "~/.emacs.d/.local/node_modules/.bin/mmdc"))

  (use-package ob-python :ensure nil :after org)
  (use-package ob-ruby :ensure nil :after org)
  (use-package ob-shell :ensure nil :after org)
  (use-package ob-sql :ensure nil :after org)

  ;; ------------ Execute Code Sample ----------------

  (org-babel-do-load-languages 'org-babel-load-languages '((C       . t)
                                                           (python  . t)
                                                           (mermaid . t)))

  (defun penguin/org-confirm-babel-evaluate (lang body)
    (not (or (string= lang "ditaa")
             (string= lang "mermaid")
             (string= lang "plantuml")
             (string= lang "dot"))))

  (setq org-confirm-babel-evaluate 'penguin/org-confirm-babel-evaluate)
#+end_src

** Presentation
A wise man has said - **org-mode like the sky, it's unlimited**. Once I work with org-mode, I felt in love with it immediately for its potiental.
Making presentation is not really required for my work, but when it is, =org-mode= with =org-reveal= & =reveal.js= saved me a lot of times.

#+BEGIN_SRC emacs-lisp :tangle yes
  (use-package ox-reveal
    :after org
    :custom
    (org-reveal-mathjax t)
    (org-reveal-root "https://cdn.jsdelivr.net/npm/reveal.js"))

  (use-package htmlize)

  (require 'ox-reveal)
#+END_SRC
